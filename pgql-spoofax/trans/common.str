module common

imports

  signatures/-
  normalized-signatures
  runtime/editor/-
  names/-

rules

  is-literal = is-literal-no-bindVariable
             + ?BindVariable(_)

  is-literal-no-bindVariable = ?Integer(_)
                             + ?Decimal(_)
                             + ?String(_)
                             + ?True() + ?False()
                             + ?Date(_) + ?Time(_) + ?Timestamp(_)

  is-numeric-literal = ?Integer(_) + ?Decimal(_)
  is-boolean-literal = ?True() + ?False()

  is-expression = ?Not(_) + ?And(_, _) + ?Or(_, _) + ?Mul(_, _) + ?Add(_, _) + ?Div(_, _) + ?Mod(_, _) +
        ?Sub(_, _) + ?Eq(_, _) + ?Lt(_, _) + ?Gte(_, _) + ?Lte(_, _) + ?Neq(_, _) + ?Legacy10Regex(_, _)
  is-function = ?Legacy10Id(_) + ?Legacy10Label(_) + ?Legacy10Has(_, _) + ?Legacy10InDegree(_) + ?Legacy10OutDegree(_) + ?Legacy10HasLabel(_, _)

  is-aggregate = ?COUNT(_, _)
               + ?MIN(_, _)
               + ?MAX(_, _)
               + ?SUM(_, _)
               + ?AVG(_, _)
               + ?ARRAY-AGG(_, _)
               + ?JSON-ARRAYAGG(_, _, _, _, _)
               + ?LISTAGG(_, _, _)

  is-quantifier = ?BetweenNAndM(_, _)
                + ?ZeroOrMore()
                + ?OneOrMore()
                + ?Optional()
                + ?ExactlyN(_)
                + ?NOrMore(_)
                + ?BetweenZeroAndM(_)

  get-exp-from-aggregation = ?COUNT(_, <id>)
                           + ?MIN(_, <id>)
                           + ?MAX(_, <id>)
                           + ?SUM(_, <id>)
                           + ?AVG(_, <id>)
                           + ?ARRAY-AGG(_, <id>)
                           + ?JSON-ARRAYAGG(<id>, _, _, _, _)
                           + ?LISTAGG(_, <id>, _)

  unique-name(|c, origin) = !origin; origin-track-forced(!Identifier(<!origin; origin-track-forced(!(<GENERATED>, "_", <next-counter; write-to-string> c); conc-strings)>, None()))

  GENERATED = !"<<anonymous>>"

  VERTEX_WITHOUT_BRACKETS = !"<<vertex-without-brackets>>"

  make-set-while-preserving-ordering = reverse; make-set; reverse

  is-subquery = ?Subquery(_)

  collect-in-outer-query(s) = collect(is-subquery <+ s); remove-all(is-subquery)

  collect-in-outer-query-outside-aggregation(s) = collect(is-subquery <+ is-aggregate <+ s); remove-all(is-subquery + is-aggregate)

  alltd-in-outer-query(s) = alltd(is-subquery <+ s)

  alltd-in-outer-query-outside-aggregation(s) = alltd(is-subquery <+ is-aggregate <+ s)

  get-aggregations = collect-in-outer-query(is-aggregate)

  to-error-message(|message) = !ErrorMessage(<origin-track-forced(origin-text)>, message)

  // just here to get rid of editor errors in e.g. normalize.str
  origin-track-forced(s) = fail
  origin-text = fail
  origin-offset = fail

  has-at-most-one-binding = ?None() + ?Some(BetweenNAndM("0", "1")); origin-text; ?"?"

  is-ddl-statement = is-CreatePropertyGraph + is-DropPropertyGraph + is-CallStatement
  is-CreatePropertyGraph = ?CreatePropertyGraph(_, _, _, _, _)
  is-DropPropertyGraph = ?DropPropertyGraph(_)
  is-CallStatement = ?CallStatement(_, _)

  norm-identifier:
    RegularIdentifier(v) -> Identifier(<origin-track-forced(upper-case)> v, v)

  norm-identifier:
    DelimitedIdentifier(v) -> Identifier(v', v')
    with v' := <origin-track-forced(un-double-quote; unescape-delimited-identifier)> v

  unescape-delimited-identifier = string-replace(|"\"\"", "\"")

  norm-string-literal = String(un-single-quote; unescape-string-literal)
  unescape-string-literal = string-replace(|"''", "'")

  to-name-without-case-sensitive-info = origin-track-forced(?Name(None(), Identifier(localName, _)); !Name(None(), localName))
  to-name-without-case-sensitive-info = origin-track-forced(?Name(Some(SchemaName(Identifier(schemaName, _))), Identifier(localName, _)); !Name(schemaName, localName))

  to-conjunction = reverse; !(<Tl>, <Hd>); foldl(to-AND)
  to-AND = ?(exp1, exp2); !And(exp1, exp2)

  to-disjunction = reverse; !(<Tl>, <Hd>); foldl(to-OR)
  to-OR = ?(exp1, exp2); !Or(exp1, exp2)

  to-disjunction-with-types = reverse; !(<Tl>, <Hd>); foldl(to-OR)
  to-OR = ?(exp1, exp2); !ExpressionPlusType(Or(exp1, exp2), Type("BOOLEAN"))

  conjunction-to-list:
    t@ExpressionPlusType(exp, _) -> result
    with if <?And(exp1, exp2)> exp
         then leftExp := <conjunction-to-list> exp1
            ; rightExp := <conjunction-to-list> exp2
            ; result := <conc> (leftExp, rightExp)
         else result := [t]
         end

  to-function(|name):
    args -> FunctionCall(None(), Identifier(name, name), args)

  simplify-label-expression(|varRefOriginOffset):
    FunctionCall(None(), Identifier(functionName, _), [ VarRef(_, varRefOriginOffset), identifierOrString]) -> label
    where <?"has_label" + ?"HAS_LABEL"> functionName
        ; label := <?Identifier(<id>, _) + ?String(<id>)> identifierOrString

  simplify-label-expression(|varRefOriginOffset):
    Or(a, b) -> LabelOr(a', b')
    where a' := <simplify-label-expression(|varRefOriginOffset)> a
        ; b' := <simplify-label-expression(|varRefOriginOffset)> b

  to-label-conjunction = reverse; !(<Tl>, <Hd>); foldl(to-label-AND)
  to-label-AND = ?(exp1, exp2); !LabelAnd(exp1, exp2)

  get-vertex-labels(|metadata) = !metadata; fetch-elem(?VertexLabels(<id>)); map(?Label(<id>, _)) <+ !None()
  get-edge-labels(|metadata) = !metadata; fetch-elem(?EdgeLabels(<id>)); map(?Label(<id>, _)) <+ !None()

  collect-vars-from-table-expression = ?GraphPattern(vertices, connections, _); !(vertices, connections)
                                     ; collect(?Vertex(_, _, None()) + ?Edge(_, _, _, _, _, None()) + ?RowsPerMatchVariable(_, _, None(), _))
                                     ; collect-om(?Edge(_, Identifier(<id>, _), _, _, _, _) <+ ?Identifier(<id>, _), conc)

  collect-vars-from-table-expression:
    DerivedTable(_, Subquery(NormalizedQuery(_, selectOrModifyClause, _, _, _, _, _, _, _, _)), _) -> vars
    with if <?SelectClause(_, SelectList(expAsVars))> selectOrModifyClause
         then vars := <filter(?ExpAsVar(_, Identifier(<id>, _), _, _))> expAsVars
         else vars := [] // will generate an error elsewhere since INSERT/UPDATE/DELETE inside LATERAL is not allowed
         end

  collect-var-def-offsets-from-graph-pattern = ?GraphPattern(vertices, connections, _); !(vertices, connections)
                                             ; collect-var-def-offsets

  collect-var-def-offsets = collect(?Vertex(_, <id>, _) + ?Edge(_, _, _, _, <id>, _) + ?RowsPerMatchVariable(_, <id>, _, _) + ?ExpAsVar(_, _, _, <id>))

  collect-offsets-from-table-expression:
    DerivedTable(_, Subquery(NormalizedQuery(_, SelectClause(_, SelectList(expAsVars)), _, _, _, _, _, _, _, _)), _) -> vars
    with vars := <filter(?ExpAsVar(ExpressionPlusType(VarRef(_, <id>), Type("VERTEX")), _, _, _) +
                         ?ExpAsVar(ExpressionPlusType(VarRef(_, <id>), Type("EDGE")), _, _, _))> expAsVars

  is-vertex-or-edge-type = ?Type("VERTEX") + ?Type("EDGE")
