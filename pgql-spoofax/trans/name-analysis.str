module name-analysis

imports

  stratego/-
  signatures/-
  normalized-signatures
  common
  pp
  runtime/index/-
  runtime/nabl/-
  runtime/properties/-
  runtime/types/-
  runtime/task/-
  runtime/analysis/-
  runtime/editor/-
  names/-

/*
 * 'trans-for-nabl' is called after 'normalize' and transforms the normalized AST into a shape that works for NaBL.
 *
 *  PGQL String -> PGQL AST -> PGQL normalized AST -> PGQL AST for NaBL -> GraphQuery object -> query plan
 */
rules

  add-names = is-ddl-statement
              + ?AstPlusMetadata(query, metadata); !AstPlusMetadata(<trans-query(|[], metadata); ?(<id>, _)> query, metadata)
              + ?Start-Plhdr() // for empty query string

rules

  trans-query(|variables, metadata):
    NormalizedQuery(CommonPathExpressions(pathExpressions), selectOrModifyClause, optionalGraphName, tableExpressions, whereClause, groupBy, having, orderBy, limitOffsets, error-messages, version, bindVariableCount, selectingAllVariables) ->
        (NormalizedQuery(CommonPathExpressions(pathExpressions'), selectOrModifyClause', optionalGraphName, tableExpressions', whereClause', groupBy', having', orderBy', limitOffsets, error-messages, version, bindVariableCount, selectingAllVariables), variables-in-projection)
    with variables' := <guarantee-size-two-or-more> variables

       // PATH
       ; pathExpressions' := <map(trans-path-expression(|variables', metadata))> pathExpressions

       // these expAsVars are used to make the column aliasevalueExpressions in SELECT visible in WHERE and GROUP BY
       ; expAsVars := <?SelectClause(_, ExpAsVars(<id>)) <+ ![]> selectOrModifyClause

       ; (tableExpressions', variables'', valueExpression') := <foldl(trans-table-expression(|metadata, expAsVars))> (tableExpressions, ([], variables', []))

         // WHERE
       ; whereClause' := <resolve-value-expression(|variables'', expAsVars, metadata)> whereClause

       // GROUP BY
       ; if (!groupBy; ?Some(_)) + (!selectOrModifyClause; create-one-group(|variables'')) + (!having; ?Some(_))
         then hasGroupBy := True()
            ; (groupBy', variables''') := <resolve-group-by(|variables'', expAsVars, metadata)> groupBy
         else hasGroupBy := False()
            ; (groupBy', variables''') := (groupBy, variables'')
         end

       // SELECT / INSERT / UPDATE / DELETE
       ; if <?SelectClause(_, _)> selectOrModifyClause
         then (selectOrModifyClause', variables'''') := <resolve-select-clause(|variables''', metadata, valueExpression')> selectOrModifyClause
         else selectOrModifyClause' := <resolve-modify-clause(|variables''', metadata)> selectOrModifyClause
           ; variables'''' := variables'''
        end

       // HAVING
       ; having' := <resolve-having(|variables''', variables'''', metadata)> having // having resolves to GROUP BY variables first, then to SELECT variables

       // ORDER BY
       // resolve to SELECT variables first, then to GROUP BY variables (except in case of a VarRef in a PropRef, in which case it is resolved to MATCH or GROUP BY first)
       ; orderBy' := < resolve-var-refs(|variables'''', metadata)
                     ; resolve-prop-refs(|variables''', metadata)
                     ; alltd(optimize-order-by)> orderBy

       // variables to pass into subsequent table expressions
       ; variables-in-projection := <diff> (<Hd> variables'''', <Hd> variables''')

// TODO fixme
//       ; if <?Some(GraphPattern(vertices', connections'', valueExpression'))> := graphPattern'
//         then connections''' := <map(try(normalize-ANY-path(|variables'''')))> connections'' // normalize ANY either to REACHES or SHORTEST
//            ; graphPattern'' := Some(GraphPattern(vertices', connections''', valueExpression'))
//         else graphPattern'' := graphPattern'
//         end

  trans-table-expression(|metadata, expAsVars):
    (GraphPattern(vertices, connections, Constraints(valueExpression)), (result, variables, valueExpressionPriorLateralQueries)) -> (result', variables', valueExpression'')
      with  // MATCH
           (vertices', connections') := <alltd(trans-elem(|variables))> (vertices, <alltd(trans-rows-per-match(|variables))> connections)
          ; edges := <filter(?Edge(_, _, _, _, _, _) + path-with-at-most-one-binding)> connections'
          ; paths := <filter(path-with-two-or-more-bindings)> connections'

          ; new-vars := <conc> (<collect(to-varDef)> (vertices', edges), <collect(rows-per-match-to-varDef)> connections')
          ; new-groupVars := <collect(to-varDef)> paths
          ; <?[x|[y|ys]]> variables
          ; visible-vars := <replace-or-add-all> (new-vars, x)
          ; visible-groupVars := <replace-or-add-all> (new-groupVars, y)
          ; variables' := [visible-vars|[visible-groupVars|ys]]

          ; connections'' := <alltd(resolve-var-refs-in-path-expression(|variables', metadata))> connections'

          // local WHERE clause for has_label constraints and future inlined constraints
          ; valueExpression' := <resolve-value-expression(|variables', expAsVars, metadata)> valueExpression

          ; graphPattern' := GraphPattern(vertices', connections'', valueExpression')
          ; result' := <conc> (result, [graphPattern'])

          ; valueExpression'' := <conc> (valueExpressionPriorLateralQueries, valueExpression') // for SELECT n.* we need to have all the label expression

  trans-table-expression(|metadata, expAsVars):
    (derivedTable@DerivedTable(lateral, subquery@Subquery(normalizedQuery)), (result, variables, valueExpressionPriorLateralQueries)) -> (result', variables', constraints')
    with (normalizedQuery', variables-in-projection) := <trans-query(|variables, metadata)> normalizedQuery
       ; subquery' := <origin-track-forced(!Subquery(normalizedQuery'))> subquery
       ; derivedTable' := <origin-track-forced(!DerivedTable(lateral, subquery'))> derivedTable
       ; result' := <conc> (result, [derivedTable'])
       ; <?[x|xs]> variables
       ; variables' := [<conc> (x, variables-in-projection)|xs]
       ; constraints' := valueExpressionPriorLateralQueries // TODO: for SELECT n.* we need to have all the label expression

  path-with-at-most-one-binding:
    t@Path(_, _, _, quantifier, _, _, _, _, _) -> t
    where <has-at-most-one-binding> quantifier

  path-with-two-or-more-bindings:
    t@Path(_, _, _, quantifier, _, _, _, _, _) -> t
    where not( <has-at-most-one-binding> quantifier )

  guarantee-size-two-or-more = ?[]; ![[], []] <+ ?[x]; ![x, []] <+ id

  trans-elem(|variables):
    Vertex(iden@Identifier(v, _)) -> Vertex(iden, origin-offset, correlation)
    with origin-offset := <origin-offset> v
       ; correlation := <to-Correlation(|variables)> v

  trans-elem(|variables):
    NormalizedEdge(src, iden@Identifier(e, _), dst, direction, _, _) -> Edge(src, iden, dst, direction, origin-offset, correlation)
    with origin-offset := <origin-offset> e
       ; correlation := <to-Correlation(|variables)> e

  to-Correlation(|variables):
    v -> correlation
    with if <Hd; oncetd(?VarDef(Identifier(v, _), _, _, _, _, _, _); ?VarDef(identifier-from-outer-query, origin-position-from-outer-query, _, _, _, _, _))> variables
         then correlation := Correlation(VarRef(identifier-from-outer-query, origin-position-from-outer-query))
         else correlation := None()
         end

  trans-rows-per-match(|variables) =
    ?OneRowPerVertex(iden@Identifier(v, _)); !OneRowPerVertex(iden, <origin-track-forced(origin-offset)> iden, <to-Correlation(|variables)> v) +
    ?OneRowPerEdge(iden@Identifier(e, _)); !OneRowPerEdge(iden, <origin-track-forced(origin-offset)> iden, <to-Correlation(|variables)> e)

  to-varDef = ?Vertex(iden, origin-offset, _); !VarDef(iden, origin-offset, None(), None(), None(), ThisQuery(), Type("VERTEX"))
  to-varDef = ?Edge(_, identifier, _, _, origin-offset, _); !VarDef(identifier, origin-offset, None(), None(), None(), ThisQuery(), Type("EDGE"))

  rows-per-match-to-varDef = ?OneRowPerVertex(iden, origin-offset, _); !VarDef(iden, origin-offset, None(), None(), None(), ThisQuery(), Type("VERTEX"))
  rows-per-match-to-varDef = ?OneRowPerEdge(iden, origin-offset, _); !VarDef(iden, origin-offset, None(), None(), None(), ThisQuery(), Type("EDGE"))

  replace-or-add-all = foldl(replace-or-add)

  replace-or-add:
    (vd@VarDef(Identifier(v, _), _, _, _, _, _, _), varDefs) -> varDefs'
    with if <fetch(?VarDef(Identifier(v, _), _, _, _, _, _, _))> varDefs
         then varDefs' := <conc> (<remove-all(?VarDef(Identifier(v, _), _, _, _, _, _, _))> varDefs, [vd])
         else varDefs' := <conc> (varDefs, [vd])
         end

  resolve-value-expression(|variables, expAsVars, metadata):
    valueExpression -> valueExpression'
    with valueExpression' := <resolve-var-refs-and-use-expAsVarsFromSelectClause(|variables, expAsVars, metadata)> valueExpression

  resolve-group-by(|variables, expAsVarsFromSelectClause, metadata):
    Some(GroupByClause(expAsVars)) -> (Some(GroupByClause(expAsVars')), variables')
    with (expAsVars', vars') := <foldl(resolve-exp-as-var-in-group-by(|variables, expAsVarsFromSelectClause, metadata))> (expAsVars, ([], []))
       ; variables' := [vars'|variables]

  resolve-group-by(|variables, expAsVars, metadata):
    None() -> (CreateOneGroup(), variables')
    with vars' := []
       ; variables' := [vars'|variables]

  resolve-select-clause(|variables, metadata, valueExpression):
    t@SelectClause(distinct, eav@ExpAsVars(expAsVars)) -> (selectClause', variables')
    with if Star() := expAsVars
         then eav' := expAsVars
            ; variables':= variables
         else varsInCurrentScope := <Hd> variables
            ; (expAsVars', vars') := <foldl(resolve-exp-as-var-in-select(|variables, metadata, valueExpression))> (expAsVars, ([], varsInCurrentScope))
            ; eav' := <origin-track-forced(!ExpAsVars(expAsVars'))> eav
            ; variables' := [vars'|<Tl> variables]
         end
       ; selectClause' := <origin-track-forced(!SelectClause(distinct, eav'))> t

  resolve-modify-clause(|variables, metadata):
    t@ModifyClause(modifications) -> modifyClause'
    with varsInCurrentScope := <Hd> variables
       ; modifications' := <map(resolve-modification(|variables, metadata))> modifications
       ; modifyClause' := <origin-track-forced(!ModifyClause(modifications'))> t

  resolve-modification(|variables, metadata):
    t@InsertClause(graphName, insertions) -> insertClause'
    with (insertions', _) := <foldl(resolve-insertion(|variables, metadata))> (insertions, ([], variables))
       ; insertClause' := <origin-track-forced(!InsertClause(graphName, insertions'))> t

  resolve-modification(|variables, metadata) = ?UpdateClause(_); resolve-var-refs(|variables, metadata)

  resolve-modification(|variables, metadata) = ?DeleteClause(_); resolve-var-refs(|variables, metadata)

  resolve-insertion(|original-variables, metadata):
    (t, (result, variables)) -> (result', variables')
    where <?VertexInsertion(iden@Identifier(v, _), labels, properties) + ?DirectedEdgeInsertion(iden@Identifier(v, _), src, dst, labels, properties)> t
    with originOffset := <origin-offset> v
       ; properties' := <resolve-set-properties(|v, originOffset, original-variables, metadata)> properties
       ; if <?VertexInsertion(_, _, _)> t
         then insertion := <origin-track-forced(!VertexInsertion(iden, originOffset, labels, properties'))> t
            ; type := Type("VERTEX")
         else src' := <resolve-var-refs(|variables, metadata)> src
            ; dst' := <resolve-var-refs(|variables, metadata)> dst
            ; insertion := <origin-track-forced(!DirectedEdgeInsertion(iden, originOffset, src', dst', labels, properties'))> t
            ; type := Type("EDGE")
         end
       ; [vars-in-scope|xs] := variables
       ; vars-in-scope' := <replace-or-add> (VarDef(iden, originOffset, None(), None(), None(), ThisQuery(), type), vars-in-scope)
       ; variables' := [vars-in-scope'|xs]
       ; result' := <conc> (result, [insertion])

  resolve-set-properties(|v, originOffset, original-variables, metadata) = ?None() + Some(Properties(map(resolve-set-property(|v, originOffset, original-variables, metadata))))

  resolve-set-property(|v, originOffset, original-variables, metadata):
    t@SetProperty(PropRef(varRef, prop), exp) -> result
    with varRef' := <?VarRef(Identifier(v, _)); ?VarRef(<id>); !VarRef(<id>, originOffset) <+ resolve-var-refs(|original-variables, metadata)> varRef
       ; exp' :=  <origin-track-forced(resolve-var-refs(|original-variables, metadata))> exp
       ; result := <origin-track-forced(!SetProperty(PropRef(varRef', prop), exp'))> t

  resolve-exp-as-var-in-group-by(|variables, expAsVarsFromSelectClause, metadata):
    (t@ExpAsVar(exp, iden@Identifier(v, _), anonymous), (result, vars)) -> (result', vars')
    with exp' := <resolve-var-refs-and-use-expAsVarsFromSelectClause(|variables, expAsVarsFromSelectClause, metadata)> exp
       ; replaced-groupByExp-with-selectExp := <alltd(?VarRef(<id>, _); !VarRef(<id>))> exp'
       ; originOffset := <origin-offset> v
       ; type := <get-type-from-varRef-as-var(|variables)> exp'
       ; vars' := <replace-or-add> (VarDef(iden, originOffset, exp, replaced-groupByExp-with-selectExp, exp', ThisQuery(), type), vars)
       ; expAsVar' := <origin-track-forced(!ExpAsVar(exp', iden, anonymous, originOffset))> t
       ; result' := <conc> (result, [expAsVar'])

  resolve-exp-as-var-in-select(|variables, metadata, valueExpression):
    (t@ExpAsVar(exp, iden@Identifier(v, _), anonymous), (result, vars)) -> (result', vars')
    with exp' := <resolve-var-refs(|variables, metadata)> exp
       ; originOffset := <origin-offset> v
       ; if <?Anonymous(); !v; origin-text; ?"*"> anonymous
         then // in case of SELECT * make sure each ExpAsVar gets a unique origin assigned
              unique-origin := <conc-strings> ("*_", <write-to-string> originOffset, "_", <write-to-string> v)
         else unique-origin := originOffset
         end
       ; type := <get-type-from-varRef-as-var(|variables)> exp'
       ; vars' := <replace-or-add> (VarDef(iden, unique-origin, exp, None(), exp', ThisQuery(), type), vars)
       ; expAsVar' := <origin-track-forced(!ExpAsVar(exp', iden, anonymous, unique-origin))> t
       ; result' := <conc> (result, [expAsVar'])

  // expression was defined in GROUP BY and doesn't need to be resolved again
  resolve-exp-as-var-in-select(|variables, metadata, valueExpression):
     (expAsVar@ExpAsVar(_, _, _, originOffset), (result, vars)) -> (result', vars')
     with vars' := vars // it was already added during GROUP BY analysis
        ; result' := <conc> (result, [expAsVar])

  resolve-exp-as-var-in-select(|variables, metadata, valueExpression):
    (t@AllProperties(varRef, columnNamePrefix), (result, vars)) -> (result', vars')
    with varRef' := <resolve-var-ref(|variables, metadata)> varRef
       ; if <?VarRef(v)> varRef'
         then expAsVars := [ExpAsVar(varRef', v, Anonymous(), None())] // create a dummy ExpAsVar just to make sure an "unresolved variable" error will be generated
            ; vars' := vars
         else originOffset := <?VarRef(_, <id>) <+ !None()> varRef'
            ; labelExpression := <filter(simplify-label-expression(|originOffset)); (?[]; !All() <+ to-label-conjunction)> valueExpression
            ; elementType := <get-type-from-varRef-as-var(|variables)> varRef'
            ; allLabels := <?Type("VERTEX"); !metadata; fetch-elem(?VertexLabels(<id>)) <+ ?Type("EDGE"); !metadata; fetch-elem(?EdgeLabels(<id>)) <+ ![]> elementType
            ; allLabelNames := <(?Type("VERTEX"); get-vertex-labels(|metadata) <+ ?Type("EDGE"); get-edge-labels(|metadata)); try(?None(); ![]) <+ ![]> elementType
            ; labelReferences := <get-labels-from-label-expression(|allLabelNames)> labelExpression
            ; properties := <filter(fetch-properties-via-label-reference(|labelReferences)); concat> allLabels
            ; propertyNames := <map(?Property(<id>, _))> properties
            ; deduplicatedPropertyNames := <make-set-while-preserving-ordering> propertyNames
            ; expAsVars := <map(property-to-exp-as-var(|t, varRef', columnNamePrefix))> deduplicatedPropertyNames
            ; vars' := <replace-or-add-all> (<map(generated-exp-as-vars-to-varDef(|elementType))> expAsVars, vars)
         end
       ; result' := <conc> (result, expAsVars)

  get-type-from-varRef-as-var(|variables):
    exp -> type
    with if <?VarRef(Identifier(v, _), _)> exp
         then type := <Hd; fetch-elem(?VarDef(Identifier(v, _), _, _, _, _, _, <id>))> variables
         else type := UnknownType()
         end

  property-to-exp-as-var(|originTerm, varRef, columnNamePrefix):
    propertyName -> <origin-track-forced(!ExpAsVar(propRef, columnIdentifier, anonymous, originOffset))> originTerm
    with propIdentifier := Identifier(<origin-track-forced(!propertyName)> originTerm, propertyName)
       ; propRef := PropRef(varRef, propIdentifier)
       ; columnName := <?Some(ColumnNamePrefix(String(<id>))); !(<id>, propertyName); conc-strings <+ !propertyName> columnNamePrefix
       ; columnIdentifier := Identifier(<origin-track-forced(!columnName)> originTerm, columnName)
       ; anonymous := NonAnonymous()
       ; originOffset := <conc-strings> ("*_", <origin-offset; write-to-string> originTerm, "_", propertyName)

  generated-exp-as-vars-to-varDef(|elementType):
    ExpAsVar(propRef, var, anonymous, originOffset) -> VarDef(var, originOffset, None(), None(), propRef, ThisQuery(), elementType)

  resolve-having(|variables-after-group-by, variables-after-select, metadata):
    having -> having'
    with [x|xs] := variables-after-select
       ; [y|_] := variables-after-group-by
       ; z := <replace-or-add-all> (y, x)
       ; variables := [z|xs]
       ; having' := <resolve-var-refs(|variables, metadata)> having

  resolve-var-refs(|variables, metadata) =
      alltd(resolve-var-ref(|variables, metadata))
    ; detect-common-subexpressions(|variables)

  /*
     First try to resolve variables as usual.
     For example, in "SELECT n.prop AS m FROM MATCH (n) -> (m) WHERE m.prop = 123", the "m" in the WHERE clause should reference the "m" in the MATCH clause.
     Only if it fails, try to replace variables with expressions from SELECT before trying to resolve again.
     For example, in "SELECT n.prop AS m FROM MATCH (n) WHERE m.prop = 123", the "m" in the WHERE clause should reference the "m" in the SELECT clause.
     Note that in the final AST, we only want the SELECT to refer to variables in GROUP BY, even though for the user it typically appears like it's the other way arround.
     For example:
        Actual query: SELECT n.age + 2 FROM MATCH (n) GROUP BY n.age + 2
        Final AST:    SELECT generatedVar FROM MATCH (n) GROUP BY n.age + 2 AS generatedVar
     For example:
        Actual query: SELECT (n.age + 2)*(n.age + 2) FROM MATCH (n) GROUP BY n.age + 2
        Final AST:    SELECT generatedVar*generatedVar FROM MATCH (n) GROUP BY n.age + 2 AS generatedVar
     For example:
        Actual query: SELECT n.age + 2 AS v FROM MATCH (n) GROUP BY v
        Final AST:    SELECT v FROM MATCH (n) GROUP BY n.age + 2 AS v
  */
  resolve-var-refs-and-use-expAsVarsFromSelectClause(|variables, expAsVarsFromSelectClause, metadata) =
      alltd-in-outer-query-outside-aggregation( resolve-var-ref-within-query(|variables, expAsVarsFromSelectClause);
                                                try(replace-ref-with-exp(|variables, expAsVarsFromSelectClause, metadata)))
    ; alltd(resolve-var-ref(|variables, metadata)) // now also resolve subqueries and also resolve to variables from outer query
    ; detect-common-subexpressions(|variables)

  detect-common-subexpressions(|variables) =
      alltd(replace-exp-with-ref-within-this-query(|variables) <+ is-subquery <+ is-aggregate)
    ; alltd(replace-exp-with-ref-from-outer-queries(|variables) <+ is-subquery <+ is-aggregate)

  resolve-var-refs-in-path-expression(|variables, metadata):
    t@Path(_, _, _, quantifier, _, _, _, _, _) -> t'
    with [_|variables'] := variables
       ; if <has-at-most-one-binding> quantifier
       then t' := <alltd(resolve-var-ref(|variables, metadata))> t // resolve only to singleton variables
       else t' := <alltd(resolve-var-ref(|variables', metadata) + resolve-var-ref(|variables, metadata))> t // resolve to either group variables or singleton variables
        end

  resolve-var-ref(|variables, metadata):
    t@VarRef(iden@Identifier(v, _)) -> varRef
    with varRef := <
           Hd; fetch(?VarDef(Identifier(v, _), origin-offset, _, _, _, _, _)); !t; origin-track-forced(!VarRef(iden, origin-offset))
           <+ !VarRef(iden)
         > variables

  // within query only, so don't resolve to variables from outer queries
  resolve-var-ref-within-query(|variables, expAsVarsFromSelectClause):
  t@VarRef(iden@Identifier(v, _)) -> varRef
    with varRef := <
           Hd; fetch(?VarDef(Identifier(v, _), origin-offset, _, _, _, ThisQuery(), _)); !VarRef(iden, origin-offset)
           <+ !VarRef(iden)
         > variables

  resolve-var-ref(|variables, metadata):
    t@PropRef(VarRef(iden@Identifier(v, _)), prop) -> PropRef(varRef, prop)
    with varRef := <
           Hd
         ; fetch(?VarDef(Identifier(v, _), origin-offset, original-exp,_ , _, _, _))

          // The original expression needs to be a vertex/edge definition (i.e. ?None()) or a reference to a vertex/edge definition (i.e. ?VarRef(_)).
          // Otherwise, it is a more complex ExpAsVar but we don't want to resolve a VarRef of a PropRef to anything other than a vertex or edge.
          // So, we leave it unresolved for now. Possibly, it will get resolved by "resolve-prop-refs" later.
         ; where ( <?None() + ?VarRef(_)> original-exp )

         ; !VarRef(iden, origin-offset)

        <+ !VarRef(iden)
      > variables

  // MIN, MAX, SUM, AVG, ...
  resolve-var-ref(|variables, metadata):
    aggr -> <origin-track-forced(!aggr')> aggr
    where <is-aggregate> aggr
    with (cons, arguments) := <explode-term> aggr
       ; variables' := <Tl> variables
       ; arguments' :=  <resolve-var-refs(|variables', metadata)> arguments
       ; aggr' := <mkterm> (cons, arguments')

  resolve-var-ref(|variables, metadata):
    Subquery(query) -> Subquery(query')
    with variables' := <alltd(VarDef(id, id, id, id, id, !OuterQuery(), id))> variables
       ; (query', _) := <trans-query(|variables', metadata)> query

  replace-exp-with-ref-within-this-query(|variables):
    exp -> varRef
    where not ( None() := exp )
        ; varRef := <Hd; filter(?VarDef(_, _,_ , _, _, ThisQuery(), _)); fetch-elem(replace-unresolved-exp-with-ref(|exp) + replace-resolved-exp-with-ref-helper(|exp))> variables

  replace-exp-with-ref-from-outer-queries(|variables):
    exp -> varRef
    where not ( None() := exp )
        ; varRef := <Hd; filter(?VarDef(_, _,_ , _, _, OuterQuery(), _)); fetch-elem(replace-unresolved-exp-with-ref(|exp))> variables

  /*
     When the expression could not be resolved, but there is an equivalent expression that we can replace it with.
     For example:
       Actual query: SELECT n.age FROM g MATCH (n) GROUP BY n.age
       Final AST:    SELECT generatedVar FROM g MATCH (n) GROUP BY n.age AS generatedVar
  */
  replace-unresolved-exp-with-ref(|exp) = ?VarDef(iden, origin-offset, original-exp, _, _, _, _); where ( <eq-ignore-case> (exp, original-exp) ); !VarRef(iden, origin-offset)

  /*
     When the expression could not be resolved, but there is an equivalent expression in the SELECT and GROUP BY.
     For example:
       Actual query: SELECT n.age AS nAge MATCH (n) GROUP BY nAge ORDER BY nAge
       Final AST:    SELECT nAge FROM g MATCH (n) GROUP BY n.age AS nAge ORDER BY nAge
  */
  replace-unresolved-exp-with-ref(|exp) = ?VarDef(iden, origin-offset, _, replaced-groupByExp-with-selectExp, _, _, _); where ( <eq-ignore-case> (exp, replaced-groupByExp-with-selectExp) ); !VarRef(iden, origin-offset)

  /*
     When the expression was resolved succesfully, but there is an equivalent expression that we can replace it with.
     For example:
       Actual query: SELECT n.age AS nAge FROM g MATCH (n) ORDER BY n.age
       Final AST:    SELECT n.age AS nAge FROM g MATCH (n) ORDER BY nAge
  */
  replace-resolved-exp-with-ref-helper(|exp) = ?VarDef(iden, origin-offset, _, _, resolved-exp, _, _); where ( <eq-ignore-case> (exp, resolved-exp) ); !VarRef(iden, origin-offset)

  eq-ignore-case = alltd(?Identifier(<id>, _)); eq

  replace-ref-with-exp(|variables, expAsVars, metadata):
    VarRef(Identifier(v, _)) -> resolved-exp
    where exp := <filter(?ExpAsVar(<id>, Identifier(v, _), NonAnonymous())); Hd> expAsVars
    with resolved-exp := <alltd(resolve-var-ref(|variables, metadata))> exp

  resolve-prop-refs(|variables, metadata) = alltd(resolve-prop-ref(|variables, metadata))

  resolve-prop-ref(|variables, metadata):
    t@PropRef(varRef@VarRef(_), prop) -> PropRef(varRef', prop)
    with varRef' := <resolve-var-ref(|variables, metadata)> varRef

  /*
     if the query has no GROUP BY but there are one ore more aggregations in the SELECT, then we generate an
     implicit group, but only if there is no (non-nested) aggregation over a group variable
  */
  create-one-group(|variables):
    t@select-or-modify-clause -> t
    with [vars|[groupVars|_]] := variables
       ; aggregations := <collect-in-outer-query(is-aggregate)> select-or-modify-clause
       ; exps-from-aggregations := <collect(get-exp-from-aggregation)> select-or-modify-clause
       ; varsInAggregations := <collect-in-outer-query-outside-aggregation(varRef-is-visible-var(|vars))> exps-from-aggregations
       ; groupVarsInAggregations := <collect-in-outer-query-outside-aggregation(varRef-is-visible-var(|groupVars))> exps-from-aggregations
       ; groupVarsInAggregations' := <diff> (groupVarsInAggregations, varsInAggregations)
    where not ( [] := aggregations ) // there are aggregations
        ; [] := groupVarsInAggregations' // but no aggregations that involve group variables (not counting nested aggregations)

  varRef-is-visible-var(|visible-vars):
    t@VarRef(Identifier(v, _)) -> t
    where <oncetd(?VarDef(Identifier(v, _), _, _, _, _, _, _))> visible-vars

  trans-path-expression(|variables, metadata):
    CommonPathExpression(name, vertices, edges, valueExpression, costClause) -> CommonPathExpression(name, vertices', edges', valueExpression', costClause')
    with
       // pattern
        (vertices', edges') := <alltd(trans-elem(|variables))> (vertices, edges)
       ; new-vars := <collect(to-varDef)> (vertices', edges')
       ; <?[x|xs]> variables
       ; visible-vars := <replace-or-add-all> (new-vars, x)

       // WHERE
       ; variables' := [visible-vars|xs]
       ; valueExpression' := <resolve-value-expression(|variables', [], metadata)> valueExpression

       ; costClause' := <resolve-value-expression(|variables', [], metadata)> costClause

  optimize-order-by:
    OrderByClause(elems) -> OrderByClause(elems')
    with elems' := <make-set-while-preserving-ordering> elems // optimization to translate e.g. ORDER BY x, y, x, y into ORDER BY x, y

  normalize-ANY-path(|variables):
    Path(src, dst, t@CommonPathExpression(_, Vertices(vertices), Edges(edges), _, _), quantifier, edge, direction, Any(), topKAnyAll, rowsPerMatch)
      -> Path(src, dst, t, quantifier, edge, direction, pathFindingGoal, topKAnyAll, rowsPerMatch)
    with groupVariables := <conc> (<map(?Vertex(_, <id>, _))> vertices, <map(?Edge(_, _, _, _, <id>, _))> edges)
       ; references := <Hd; collect(?VarRef(_, <id>))> variables
       ; referencedGroupVariables := <isect> (groupVariables, references)
       ; if <?[]; !rowsPerMatch; (?None() + ?Some(OneRowPerMatch()))> referencedGroupVariables
         then pathFindingGoal := Reaches()
         else pathFindingGoal := Shortest()
         end

rules

  get-labels-from-label-expression(|allLabels):
    labelExpression -> result
    where <is-string> labelExpression
    with result := [labelExpression]

  get-labels-from-label-expression(|allLabels):
    LabelAnd(a, b) -> result
    with result := <get-union-of-labels(|allLabels)> (a, b)

  get-labels-from-label-expression(|allLabels):
    LabelOr(a, b) -> result
    with result := <get-union-of-labels(|allLabels)> (a, b)

  get-union-of-labels(|allLabels):
    (labelExpressionA, labelExpressionB) -> result
    with labelsA := <get-labels-from-label-expression(|allLabels)> labelExpressionA
       ; labelsB := <get-labels-from-label-expression(|allLabels)> labelExpressionB
       ; result := <union> (labelsA, labelsB)

  get-labels-from-label-expression(|allLabels):
    All() -> allLabels

  fetch-properties-via-label-reference(|labelReferences):
    Label(labelName, properties) -> properties
    where <fetch-elem(?labelName)> labelReferences

  fetch-properties-via-label-reference(|labelReferences):
    Label(labelName, properties) -> properties
    where upperCasedLabelName := <upper-case> labelName
        ; <fetch-elem(?upperCasedLabelName)> labelReferences
