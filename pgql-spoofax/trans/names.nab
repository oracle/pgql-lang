module names

imports

  signatures/-

namespaces

  CommonPathExpression // regular PATH pattern
  Var

properties

  OriginOffset of Var

binding rules // scoping

  NormalizedQuery(_, _, _, _, _, _, _, _, _, _):
    scopes Var, CommonPathExpression

binding rules // variable definition

  CommonPathExpression(p, _, _, _):
    defines unique CommonPathExpression p
    scopes Var

  Vertex(n, origin-offset):
    defines non-unique Var n
      of OriginOffset origin-offset
      of type VertexTy()

  Edge(_, e, _, _, origin-offset):
    defines unique Var e
      of OriginOffset origin-offset
      of type EdgeTy()

  Path(_, _, _, _, p, _):
    defines unique Var p
      of type PathTy()

  ExpAsVar(exp, v, _, _, origin-offset):
    defines unique Var v // unlike SQL, we currently don't allow duplicate column names. This means that we eagerly detect potential ambiguous references). We may relax this in the future.
      of OriginOffset origin-offset
      of type ty where exp has type ty

binding rules // variable reference

  PathPatternRef(p):
    refers to CommonPathExpression p

  VarRef(v, _, origin-offset):
    refers to Var v
      of OriginOffset origin-offset
