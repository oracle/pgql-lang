module names

imports

  signatures/-

namespaces

  CommonPathExpression // regular PATH pattern
  Var

properties

  OriginOffset of Var

binding rules // scoping

  NormalizedQuery(_, _, _, _, _, _, _, _, _, _):
    scopes Var, CommonPathExpression

binding rules // variable definition

  CommonPathExpression(Identifier(p, _), _, _, _, _):
    defines unique CommonPathExpression p

  Vertex(Identifier(n, _), origin-offset, _):
    defines non-unique Var n
      of OriginOffset origin-offset
      of type VertexTy()

  Edge(_, Identifier(e, _), _, _, origin-offset, _):
    defines unique Var e
      of OriginOffset origin-offset
      of type EdgeTy()

  Path(_, _, _, _, Identifier(p, _), _, _, _, _, _):
    defines unique Var p
      of type PathTy()

  ExpAsVar(exp, Identifier(v, _), _, origin-offset):
    defines unique Var v // unlike SQL, we currently don't allow duplicate column names. This means that we eagerly detect potential ambiguous references). We may relax this in the future.
      of OriginOffset origin-offset

  VertexInsertion(Identifier(v, _), origin-offset, _, _):
    defines unique Var v
      of OriginOffset origin-offset
      of type VertexTy()

  DirectedEdgeInsertion(Identifier(v, _), origin-offset, _, _, _, _):
    defines unique Var v
      of OriginOffset origin-offset
      of type EdgeTy()

  RowsPerMatchVariable(Identifier(v, _), origin-offset, _, Type("VERTEX")):
    defines unique Var v
      of OriginOffset origin-offset
      of type VertexTy()

  RowsPerMatchVariable(Identifier(v, _), origin-offset, _, Type("EDGE")):
    defines unique Var v
      of OriginOffset origin-offset
      of type EdgeTy()

binding rules // variable reference

  VarRef(Identifier(v, _), origin-offset):
    refers to Var v
      of OriginOffset origin-offset
