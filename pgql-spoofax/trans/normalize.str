module normalize

imports

  stratego/-
  signatures/-
  normalized-signatures
  common
  pp
  runtime/index/-
  runtime/nabl/-
  runtime/properties/-
  runtime/types/-
  runtime/task/-
  runtime/analysis/-
  runtime/editor/-
  names/-
  normalize-ddl

rules

  // CREATE PROPERTY GRAPH
  normalize-before:
    ast -> <normalize-CreatePropertyGraph> ast
    where <is-CreatePropertyGraph> ast

  // DROP PROPERTY GRAPH
  normalize-before:
    ast -> <normalize-DropPropertyGraph> ast
    where <is-DropPropertyGraph> ast

  // CREATE EXTERNAL SCHEMA
  normalize-before:
    ast -> <normalize-CreateExternalSchema> ast
    where <is-CreateExternalSchema> ast

  // DROP EXTERNAL SCHEMA
  normalize-before:
    ast -> <normalize-DropExternalSchema> ast
    where <is-DropExternalSchema> ast

  // SELECT, INSERT, UPDATE or DELETE
  normalize-before:
    astPlusMetadata -> AstPlusMetadata(result, metadata)
    where <not(is-ddl-statement)> astPlusMetadata
    with
      ast := <try(?AstPlusMetadata(<id>, _))> astPlusMetadata; // AST does not always come with metadata
      metadata := <?AstPlusMetadata(_, <id>) <+ ![]> astPlusMetadata;
      variable-counter := <new-counter>;
      bindVariable-counter := <new-counter>;
      error-messages := <
        (  is-pgql10; collect(get-pgql10-limitation)
        <+ is-pgql11; (collect(get-pgql11-limitation); not(?[]) <+ collect(get-pgql11-deprecation))
        <+ is-pgql13; collect(get-pgql13-deprecation + get-pgql13-errors); flatten-list
        )
        ; not(?[]) <+ get-path-limitations; not(?[])
          <+ get-errors-for-inconsistent-graph-names <+ ![]> ast;
      result := <
        alltd(norm-has-prop);
        alltd(norm-pgql10);
      	alltd(norm-BindVariable(|bindVariable-counter));
      	alltd(norm-UMin);
        innermost(
          norm-NOT +
          norm-NEQ +
          norm-singleVertex +
          norm-VertexWithoutBrackets +
          norm-isNull +
          norm-case
        );
      	alltd(norm-edgeContents);
        ( (is-pgql10 + is-pgql11); alltd((?PropRef(_, _) <+ varRef-to-legacy-string-literal) + norm-legacy-string-literal); alltd(norm-legacy-identifier) +
          is-pgql13; alltd(norm-string-literal + norm-identifier) +
          ?Start-Plhdr() // empty query string
        );
        alltd(norm-timezone);
        alltd(norm-datetime-literal);
        alltd(norm-hop-distance);
        alltd(name-unnamed-elems(|variable-counter));
        alltd(norm-inlined-references);
        bottomup(try(
           norm-matchElems(|variable-counter)
        ));
        bottomup(try(origin-track-forced(norm-query(|error-messages, variable-counter, <get-counter> bindVariable-counter))));
        alltd(?ElemContents(Some(<id>), _, _, _));
        try(copy-graph-name-to-queries-without-one)
      > ast

rules

  is-pgql10 = ?Pgql10Query(_, _, _, _, _, _, _)
  is-pgql11 = ?Pgql11Query(_, _, _, _, _, _, _, _)
  is-pgql13 = ?Query(_, _, _, _, _, _, _)

  get-pgql11-deprecation = (?OutConn(None(), _) + ?InConn(None(), _) + ?UndirectedEdge(None(), _)); to-error-message(|"Use [-] instead of [--]")
  get-pgql11-deprecation = ?Pgql10AsignSymbol(); to-error-message(|"Use [AS] instead of [:=]")
  get-pgql11-deprecation = ?Legacy10Label(_); to-error-message(|"Use [label(x)] instead of [x.label()]")
  get-pgql11-deprecation = ?Legacy10Labels(_); to-error-message(|"Use [labels(x)] instead of [x.labels()]")
  get-pgql11-deprecation = ?Legacy10HasLabel(_, _); to-error-message(|"Use [has_label(x, 'lbl')] instead of [x.hasLabel('lbl')]")
  get-pgql11-deprecation = ?Legacy10Id(_); to-error-message(|"Use [id(x)] instead of [x.id()]")
  get-pgql11-deprecation = ?Legacy10Has(_, _); to-error-message(|"Use [x.prop IS NOT NULL] instead of [x.has(prop)]")
  get-pgql11-deprecation = ?Legacy10InDegree(_); to-error-message(|"Use [in_degree(x)] instead of [x.indegree()]")
  get-pgql11-deprecation = ?Legacy10OutDegree(_); to-error-message(|"Use [out_degree(x)] instead of [x.out_degree()]")

  get-pgql11-deprecation = ?Legacy10Asc(x); to-error-message(|$<Use [<<origin-text> x> ASC] instead of [ASC(<<origin-text> x>)]>)
  get-pgql11-deprecation = ?Legacy10Desc(x); to-error-message(|$<Use [<<origin-text> x> DESC] instead of [DESC(<<origin-text> x>)]>)
  get-pgql11-deprecation = ?Legacy10Identifier(_); to-error-message(|"Use double quotes to delimit labels")
  get-pgql11-deprecation = ?Ids(_); to-error-message(|"Use [id(x) = xyz] instead of [@xyz]")
  get-pgql11-deprecation = ?InlinedConstraint(_); to-error-message(|"Constraints should be specified in the WHERE clause instead of the WITH")
  get-pgql11-deprecation = ?Legacy10Not(_); to-error-message(|"Use [NOT] instead of [!]")
  get-pgql11-deprecation = ?Legacy10Regex(_, _); to-error-message(|"Use [java_regexp_like(exp1, exp2)] instead of [exp1 =~ exp2]")

  get-pgql13-deprecation = get-pgql11-deprecation // sharing all the deprecations with 1.1

  get-pgql13-errors      = ?GraphMatch(None(), _, _); to-error-message(|"Missing MATCH keyword at start of pattern")

  get-pgql10-limitation = ( ?Pgql11FromClause(_)
                          + ?Subquery(_)
                          + ?Pgql11AsignSymbol()
                          + ?ExtractExp(_, _)
                          + ?ARRAY-AGG(_, _)
                          + ?AnyPath(_)
                          + ?AllPaths(_)
                          + ?Shortest(_, _)
                          + ?Cheapest(_, _)
                          )
                        ; to-error-message(|"Use [SELECT .. FROM MATCH .. WHERE ..]")

  get-pgql11-limitation = ?VarRef(DelimitedIdentifier(<id>)); to-error-message(|"Double quoted variable references are only available in PGQL 1.3 and up")
                        + ?ModifyClause(_); to-error-message(|"INSERT, UPDATE and DELETE are only supported in PGQL 1.3 and up (use FROM MATCH ...)")

  // TODO: move this to check.str (all the information should be in the normalized AST)
  get-path-limitations = collect(quantified-path-pattern + is-quantifier)
                       ; remove-all(quantified-path-pattern)
                       ; not(?[])
                       ; map(to-error-message(|"Quantifier needs to be enclosed by ANY, ALL, SHORTEST or CHEAPEST"))

  get-path-limitations = collect(?Shortest(_, _) + ?Cheapest(_, _) + ?AnyPath(_) + ?AllPaths(_)); collect(?Path(_, _, _)); not(?[]); map(to-error-message(|"Nested regular expressions not supported"))

  get-errors-for-inconsistent-graph-names:
    ast -> <conc> (errors-for-multiple-graph-name, errors-for-missing-on-clauses)
    with errors-for-multiple-graph-name := <get-errors-for-multiple-graph-names> ast
       ; if <is-pgql10 + is-pgql11> ast
         then errors-for-missing-on-clauses := [] // before PGQL 1.3 we didn't have these error yet
         else errors-for-missing-on-clauses := <get-errors-for-missing-on-clauses> ast
         end

  get-errors-for-multiple-graph-names:
    ast -> errors
    with inputGraphNames := <collect-om(get-on-clause-graph-name; alltd(norm-identifier); to-name-without-case-sensitive-info)> ast
       ; numInputGraphNames := <length> inputGraphNames
       ; if <gt> (numInputGraphNames, 1)
         then errors := <map(to-error-message(|"Querying multiple graphs is not supported"))> inputGraphNames
         else errors := []
         end

  get-on-clause-graph-name = ?GraphMatch(_, _, Some(OnClause(<id>))) + ?ParenthesizedGraphMatch(_, Some(OnClause(<id>)))

  get-errors-for-missing-on-clauses:
    ast -> errors
    with if <oncetd(?OnClause(_))> ast
         then clausesWithMissingGraphName := <collect(?GraphMatch(_, _,  None()) + ?ParenthesizedGraphMatch(_, None()))> ast
            ; errors := <map(to-error-message(|"Missing ON clause"))> clausesWithMissingGraphName
         else errors := []
         end

  quantified-path-pattern = ?Shortest(_, _)
                          + ?Cheapest(_, _)
                          + ?AnyPath(_)
                          + ?AllPaths(_)
                          + ?OutConn(Some(Path(_, _, _)), _)
                          + ?InConn(Some(Path(_, _, _)), _)

  norm-pgql10 = ?Legacy10Label(x); <to-function(|"label")> [x]
  norm-pgql10 = ?Legacy10Labels(x); <to-function(|"labels")> [x]
  norm-pgql10 = ?Legacy10HasLabel(x, lbl); <to-function(|"has_label")> [x, lbl]
  norm-pgql10 = ?Legacy10Id(x); <to-function(|"id")> [x]
  norm-pgql10 = ?Legacy10Has(x, prop); <to-function(|"has_prop")> [x, prop]
  norm-pgql10 = ?Legacy10InDegree(x); <to-function(|"in_degree")> [x]
  norm-pgql10 = ?Legacy10OutDegree(x); <to-function(|"out_degree")> [x]
  norm-pgql10 = ?Legacy10Regex(x, regex); <to-function(|"java_regexp_like")> [x, regex]

  // In PGQL 1.1 and PGQL 1.2, subqueries would inherit the graph name from the outer query.
  // This is no longer the case in PGQL 1.3+, where graph names in subqueries need to be respecified.
  copy-graph-name-to-queries-without-one:
    ast@NormalizedQuery(_, _, _, _, _, _, _, _, _, "v1.1", _) -> ast'
    where <oncetd(?NormalizedQuery(_, _, Some(graphName), _, _, _, _, _, _, _, _))> ast
    with ast' := <innermost(copy-graph-name-to-query-without-one(|graphName))> ast

  copy-graph-name-to-query-without-one(|graphName):
    NormalizedQuery(a, b, None(), c, d, e, f, g, h, i, j) -> NormalizedQuery(a, b, Some(graphName), c, d, e, f, g, h, i, j)

  norm-BindVariable(|c) = ?BindVariable(); !BindVariable(<next-counter> c)

  norm-UMin = ?UMin(Integer(string)); !Integer(<conc-strings> ("-", string))
  norm-UMin = ?UMin(Decimal(string)); !Decimal(<conc-strings> ("-", string))

  norm-NOT = ?Legacy10Not(<id>); !Not(<id>)

  norm-NEQ = ?Neq1(exp1, exp2); !Neq(exp1, exp2)
  norm-NEQ = ?Neq2(exp1, exp2); !Neq(exp1, exp2)

  norm-legacy-string-literal:
    String(s) -> String(s')
    with s' := <origin-track-forced(un-single-quote; unescape-legacy-string-literal)> s

  varRef-to-legacy-string-literal:
    VarRef(DelimitedIdentifier(s)) -> String(s')
    with s' := <origin-track-forced(un-double-quote; unescape-legacy-identifier)> s

  norm-legacy-identifier:
    RegularIdentifier(v) -> Identifier(v, v)

  norm-legacy-identifier:
    DelimitedIdentifier(v) -> Identifier(v', v')
    with v' := <origin-track-forced(un-double-quote; unescape-legacy-identifier)> v

  // (n:'lbl')
  norm-legacy-identifier:
    Legacy10Identifier(v) -> Legacy10Identifier(v')
    with v' := <origin-track-forced(un-single-quote; unescape-legacy-string-literal)> v

  external unescape-legacy-string-literal(|)
  external unescape-legacy-identifier(|)

  norm-datetime-literal = Date(un-single-quote)
                        + Time(un-single-quote)
                        + Timestamp(un-single-quote)

  norm-timezone:
    Cast(exp, type) -> Cast(exp, type')
    with
      type' := <origin-track-forced(upper-case
              /* In standard SQL the time zone is denoted by "TIME ZONE" while in java.sql.* it's denoted by "TIMEZONE".
                 Clearly people have been confusing it. Therefore, we support both notations in PGQL by normalizing one
                 into the other. Users can then use either. However, please stick to TIME ZONE (with space) in
                 documentation to follow the standard. */
              ; string-replace(|"TIME ZONE", "TIMEZONE"))> type

  norm-isNull = ?IsNotNull(exp); origin-track-forced(!IsNull(exp)); !Not(<id>)

  norm-case:
    t@SimpleCase(caseOperand, whenClauses, elseClause) -> SimpleCase(caseOperand, whenClauses, elseClause, ifElseRepresentation)
    with ifElseRepresentation := <to-if-else(|t)> (caseOperand, whenClauses, elseClause)

  to-if-else(|origin-term):
    (caseOperand, whenClauses, elseClause) -> ifElse
    with [WhenClause(whenExp, thenExp)|xs] := whenClauses
       ; exp1 := Eq(caseOperand, whenExp)
       ; exp2 := thenExp
       ; if [] := xs
         then exp3 := <to-else> elseClause
         else exp3 := <to-if-else(|elseClause)> (caseOperand, xs, elseClause)
         end
       ; ifElse := <origin-track-forced(!IfElse(exp1, exp2, exp3))> origin-term

  norm-case:
    t@SearchedCase(whenClauses, elseClause) -> ifElse
    with ifElse := <to-if-else(|t)> (whenClauses, elseClause)
       ; <origin-text> t

  to-if-else(|origin-term):
    (whenClauses, elseClause) -> ifElse
    with [WhenClause(whenExp, thenExp)|xs] := whenClauses
       ; exp1 := whenExp
       ; exp2 := thenExp
       ; if [] := xs
         then exp3 := <to-else> elseClause
         else [WhenClause(_, thenExp2)|_] := xs
            ; exp3 := <to-if-else(|thenExp2)> (xs, elseClause)
         end
       ; ifElse := <origin-track-forced(!IfElse(exp1, exp2, exp3))> origin-term

  to-else = ?None(); !Null() <+ ?Some(ElseClause(<id>))

rules

  norm-singleVertex = ?SingleVertex(elemContents); !PathPattern(Vertex(elemContents), []) // SELECT * WHERE ()

  norm-VertexWithoutBrackets = ?VertexWithoutBrackets(v); !Vertex(ElemContents(Some(RegularIdentifier(<origin-track-forced(<conc-strings> (v, <VERTEX_WITHOUT_BRACKETS>))>)), None(), None(), None()))

  norm-edgeContents = ?Some(EdgeContents(<id>))

  name-unnamed-elems(|c) = (?CommonPathExpression(_, _, _, _, _) + ?GraphPattern(_) + ?MatchWhereClauses(_, _) + ?PathPattern(_, _)); alltd(name-unnamed-elem(|c))
  name-unnamed-elem(|c) = ?ElemContents(None(), type, id', constraints); ?origin; !ElemContents(Some(<unique-name(|c, origin)>), type, id', constraints)
  name-unnamed-elem(|c) = ?origin; (OutConn(?None(); name-unnamed-elem-helper(|c, origin), id) +
                           InConn(?None(); name-unnamed-elem-helper(|c, origin), id) +
                           UndirectedEdge(?None(); name-unnamed-elem-helper(|c, origin), id))
  name-unnamed-elem(|c) = ?OutEdge(quantifier); ?origin; !OutConn(<name-unnamed-elem-helper(|c, origin)>, quantifier) +
                          ?InEdge(quantifier); ?origin; !InConn(<name-unnamed-elem-helper(|c, origin)>, quantifier) +
                          ?UndirectedEdge(quantifier); ?origin; !UndirectedEdge(<name-unnamed-elem-helper(|c, origin)>, quantifier) +
                          ?Path(None(), pathPatternRef, kleeneStar); ?origin; !Path(Some(<unique-name(|c, origin)>), pathPatternRef, kleeneStar)
  name-unnamed-elem-helper(|c, origin) = !ElemContents(Some(<unique-name(|c, origin)>), None(), None(), None())

  norm-inlined-references = ?ElemContents(Some((varName)), _, _, _); alltd(norm-inlined-reference(|varName))
  norm-inlined-reference(|varName) =
    ?Obj(dot); !VarRef(varName) + (?Obj(VarRef(v), dot) + ?PropRef(VarRef(v), _));
    if <eq> (v, varName)
    then !ReferenceInInlinedConstraint(v)
    else !CrossReferenceInInlinedConstraint(v)
    end +
    (?SelfPropRef(propName) + ?SelfPropRef(dot, propName)); !PropRef(VarRef(varName), Identifier(propName, propName))

rules

  // PGQL 1.3
  norm-matchElems(|variable-counter):
    FromWhereClauses(graphMatches, whereClause) -> GraphPatternAndGraphName(graphPattern, graphName, whereClause)
    with graphName := <map(?GraphMatch(_, _, <id>) + ?ParenthesizedGraphMatch(_, <id>)); (Hd; normalize-OnClause <+ !None())> graphMatches // simply take the first graph name we see; we don't support multiple graph right now
       ; pathPatterns := <map(?GraphMatch(_, <id>, _); ![<id>] + ?ParenthesizedGraphMatch(<id>, _)); concat> graphMatches
       ; graphPattern := <norm-matchElems-common(|variable-counter)> pathPatterns

  // PGQL 1.1 and 1.2
  norm-matchElems(|variable-counter):
    MatchWhereClauses(elems, whereClause) -> GraphPattern(vertices, connections, constraints)
    with <?GraphPattern(vertices, connections, Constraints(inlined-constraints))> := <norm-matchElems-common(|variable-counter)> elems
       ; non-inlined-constraints := <?Some(WhereClause(<id>)); ![<id>] + ?None(); ![]> whereClause
       ; constraints := Constraints(<conc> (inlined-constraints,  non-inlined-constraints))

  // PGQL 1.0
  norm-matchElems(|variable-counter):
    GraphPattern(elems) -> graphPattern
    with graphPattern := <norm-matchElems-common(|variable-counter)> elems

  norm-matchElems-common(|variable-counter):
    matchElems -> GraphPattern(vertices, connections, constraints)
    with pathPatterns := <remove-all(?Constraint(_))> matchElems // relevant to PGQL 1.0 only
       ; vertices := <collect-outside-repeated-path(?Vertex(ElemContents(Some(<id>), _, _, _)); !Vertex(<id>)); remove-duplicate-vertices; !Vertices(<id>)> pathPatterns
       ; connections := <get-connections-from-paths(|variable-counter)> pathPatterns

       ; non-inlined-constraints := <filter(?Constraint(<id>))> matchElems // PGQL 1.0 only
       ; inlined-constraints-for-vertices := <collect-outside-repeated-path(?Vertex(<id>))
                                           ; map(get-inlined-constraints); concat> pathPatterns
       ; inlined-constraints-for-edges := <collect-outside-repeated-path(?OutConn(<id>, None())
                                                                       + ?InConn(<id>, None())
                                                                       + ?UndirectedEdge(<id>, None()))
                                        ; filter(get-inlined-constraints); concat> pathPatterns
       ; constraints := <conc; flatten-list; !Constraints(<id>)> (inlined-constraints-for-vertices, inlined-constraints-for-edges, non-inlined-constraints)

  normalize-OnClause = ?Some(OnClause(<id>)); !Some(GraphName(<id>))

  collect-outside-repeated-path(s) = collect-but-preserve-order(ParenthesizedPath + s); remove-all(ParenthesizedPath)

  collect-but-preserve-order(s) = collect-om(s, reverse-then-union); reverse // to preserve order of vertices

  reverse-then-union:
    (x, y) -> <union> (y, x)

  // ignore origin text so that we don't end up with duplicate vertices because of case insensitivity
  // Vertex(Identifier("N","n")),Vertex(Identifier("N","N")) ==> Vertex(Identifier("N","n")))
  remove-duplicate-vertices:
    vertices -> vertices'
    where not ( <?[]> vertices )
    with init := <init> vertices
       ; last@Vertex(Identifier(n, _)) := <last> vertices
       ; if <fetch(?Vertex(Identifier(n, _)))> init
         then vertices' := <remove-duplicate-vertices> init
         else vertices' := <conc> (<remove-duplicate-vertices> init, [last])
         end

  remove-duplicate-vertices = ?[] // base case; empty list

  ParenthesizedPath = ?ParenthesizedPath(_, _, _, _)

  norm-common-path-expression(|variable-counter):
    CommonPathExpression(name, _, path, whereClause, costClause) -> CommonPathExpression(name, vertices, connections, constraints, costClause)
    with
      inlined-constraints-for-vertices := <collect-om(?Vertex(<id>)); map(get-inlined-constraints); concat> path;
      inlined-constraints-for-edges := <collect-om(?OutConn(<id>, _) + ?InConn(<id>, _) + ?UndirectedEdge(<id>, _)); filter(get-inlined-constraints); concat> path;
      non-inlined-constraints := <(?None(); ![]) <+ (?Some(WhereClause(<id>)); ![<id>])> whereClause;
      constraints := <concat; flatten-list; !Constraints(<id>)> [inlined-constraints-for-vertices, inlined-constraints-for-edges, non-inlined-constraints];
      vertices := <collect-om(?Vertex(_), conc); !Vertices(<id>)> path;
      connections := <get-connections-from-paths(|variable-counter); !Edges(<id>)> [path]

  get-inlined-constraints:
    ElemContents(Some(var), label, id', otherConstraint) -> <concat> [labels, identifiers, otherConstraint']
    with labels := <?Some(Labels(<id>)); to-label-constraint(|var); ![<id>] <+ ![]> label
       ; identifiers := <?Some(Ids(<id>)); to-id-constraint(|var); ![<id>] <+ ![]> id'
       ; otherConstraint' := <?Some(InlinedConstraint(<id>)) <+ ![]> otherConstraint

  to-label-constraint(|var):
    labels -> <to-hasLabel-expressions(|var)> labels
  to-hasLabel-expressions(|var) = bottomup(try(![VarRef(var), <to-label>]; to-function(|"has_label") + origin-track-forced(normalize-label-disjunction)))
  to-label = ?Label(<id>) + ?Legacy10Identifier(<id>); origin-track-forced(!Identifier(<id>, <id>)) + ?BindVariable(_)
  to-id-constraint(|var) = bottomup(try(is-literal; origin-track-forced(!Eq(<to-function(|"id")> [VarRef(var)], <id>))))
  normalize-label-disjunction = ?LabelDisjunction(x, y); !Or(x, y)

  // base case: no paths
  get-connections-from-paths(|variable-counter):
    [] -> []

  // head is path with only one vertex => get neighbors for the other paths
  get-connections-from-paths(|variable-counter):
    [PathPattern(_, [])|otherPaths] -> <get-connections-from-paths(|variable-counter)> otherPaths

  // paths with more than one vertex
  get-connections-from-paths(|variable-counter):
    [pathPattern|otherPaths] -> <conc> (edges, edgesTailPaths)
    where not ( <?Shortest(_, _) + ?Cheapest(_, _) + ?AnyPath(_) + ?AllPaths(_)> pathPattern)
    with topKAnyAll := None()
       ; pathFindingGoal := None()
       ; edges := <pathPattern-to-connections(|variable-counter, pathFindingGoal, topKAnyAll)> pathPattern
       ; edgesTailPaths := <get-connections-from-paths(|variable-counter)> otherPaths

  // paths with more than one vertex
  get-connections-from-paths(|variable-counter):
    [Shortest(topKAnyAll, pathPattern)|otherPaths] -> result
    with result := <get-connections-from-quantified-path-patterns(|variable-counter)> (Shortest(), topKAnyAll, pathPattern, otherPaths)

  // paths with more than one vertex
  get-connections-from-paths(|variable-counter):
    [Cheapest(topKAnyAll, pathPattern)|otherPaths] -> result
    with result := <get-connections-from-quantified-path-patterns(|variable-counter)> (Cheapest(), topKAnyAll, pathPattern, otherPaths)

  // paths with more than one vertex
  get-connections-from-paths(|variable-counter):
    [AnyPath(pathPattern)|otherPaths] -> result
    with result := <get-connections-from-quantified-path-patterns(|variable-counter)> (Any(), None(), pathPattern, otherPaths)

  // paths with more than one vertex
  get-connections-from-paths(|variable-counter):
    [AllPaths(pathPattern)|otherPaths] -> result
    with result := <get-connections-from-quantified-path-patterns(|variable-counter)> (All(), None(), pathPattern, otherPaths)

  get-connections-from-quantified-path-patterns(|variable-counter):
    (pathFindingGoal, topKAnyAll, pathPattern, otherPaths) ->  <conc> ([path'], edgesTailPaths)
    with edges := <pathPattern-to-connections(|variable-counter, pathFindingGoal, topKAnyAll)> pathPattern
       ; path' := <?[single-path-only]; !single-path-only; try(single-edge-to-path(|variable-counter, pathFindingGoal, topKAnyAll)) <+ !ComplexRegularExpressionNotSupported(<id>)> edges
       ; edgesTailPaths := <get-connections-from-paths(|variable-counter)> otherPaths

  single-edge-to-path(|variable-counter, pathFindingGoal, topKAnyAll):
    edge@NormalizedEdge(srcVertex, e, dstVertex, direction, quantifier, constraints) -> path
    with costClause := None()
       ; path := <to-path(|variable-counter, pathFindingGoal, topKAnyAll)> (srcVertex, dstVertex, edge, constraints, costClause, quantifier)

  pathPattern-to-connections(|variable-counter, pathFindingGoal, topKAnyAll):
    PathPattern(Vertex(n1), edgeVertices@[EdgeVertex(e, Vertex(n2))|_]) -> <conc> ([edge], edgesPathTail)
    with edge := <to-connection(|variable-counter, pathFindingGoal, topKAnyAll)> (n1, e, n2)
       ; edgesPathTail := <get-connections-from-single-path(|variable-counter, pathFindingGoal, topKAnyAll)> edgeVertices

  // base case: only one edgeVertex
  get-connections-from-single-path(|variable-counter, pathFindingGoal, topKAnyAll):
    [EdgeVertex(_, _)] -> []
    
  // two or more edgeVertices
  get-connections-from-single-path(|variable-counter, pathFindingGoal, topKAnyAll):
    [EdgeVertex(_, Vertex(n1)) | edgeVertices@[EdgeVertex(e, Vertex(n2))|_]]
    -> <conc> ([edge], edgesPathTail)
    with edge := <to-connection(|variable-counter, pathFindingGoal, topKAnyAll)> (n1, e, n2)
       ; edgesPathTail := <get-connections-from-single-path(|variable-counter, pathFindingGoal, topKAnyAll)> edgeVertices

  to-connection(|variable-counter, pathFindingGoal, topKAnyAll):
    (n1, edgeOrPath, n2) -> connection
    where <?OutConn(_, quantifier) + ?InConn(_, quantifier) + ?UndirectedEdge(_, quantifier)> edgeOrPath
    with
      switch !edgeOrPath
        case ?OutConn(edgeOrPath', _):
          src := n1;
          dst := n2;
          dir := Outgoing()
        case ?InConn(edgeOrPath', _):
          src := n2;
          dst := n1;
          dir := Incoming()
        case ?UndirectedEdge(edgeOrPath', _):
          src := n1;
          dst := n2;
          dir := Undirected()
        otherwise: fail
      end;
      switch !edgeOrPath'
        case ?ElemContents(Some(e'), _, _, _):
          constraints := <get-inlined-constraints> edgeOrPath'
        ; if <has-at-most-one-binding> quantifier
          then connection := <origin-track-forced(!NormalizedEdge(src, e', dst, dir, quantifier, Constraints(constraints)))> edgeOrPath
          else srcVertex := <unique-name(|variable-counter, edgeOrPath)>
             ; dstVertex := <unique-name(|variable-counter, edgeOrPath)>
             ; edge := <origin-track-forced(!NormalizedEdge(srcVertex, e', dstVertex, dir, None(), Constraints([])))> edgeOrPath
             ; costClause := None()
             ; connection := <to-path(|variable-counter, pathFindingGoal, topKAnyAll)> (n1, n2, edge, Constraints(constraints), costClause, quantifier)
          end
        case ?Some(Path(Some(pathName), pathPatternName, quantifier')):
          connection := Path(src, dst, pathPatternName, quantifier', pathName, dir, Reaches(), -1)
        otherwise: fail
      end

  to-connection(|variable-counter, pathFindingGoal, topKAnyAll):
    (n1, t@ParenthesizedPath(RelaxedPathPattern(v1, [RelaxedEdgeVertex(edge, v2)|otherEdges]), whereClause, costClause, quantifier), n2) -> path
    with if <has-at-most-one-binding> quantifier
         then srcVertex := <?Some(Vertex(<id>)) <+ ?None(); !n1> v1
            ; dstVertex := <?Some(Vertex(<id>)) <+ ?None(); !n2> v2
         else srcVertex := <?Some(Vertex(<id>)) <+ ?None(); unique-name(|variable-counter, edge)> v1
            ; dstVertex := <?Some(Vertex(<id>)) <+ ?None(); unique-name(|variable-counter, edge)> v2
         end
       ; edge' := <to-connection(|variable-counter, pathFindingGoal, topKAnyAll)> (srcVertex, edge, dstVertex)
       ; inlinedEdgeConstraints := <?NormalizedEdge(_, _, _, _, _, Constraints(<id>))> edge'
       ; inlinedSrcVertexConstraints := <?Some(Vertex(<id>)); get-inlined-constraints <+ ![]> v1
       ; inlinedDstVertexConstraints := <?Some(Vertex(<id>)); get-inlined-constraints <+ ![]> v2
       ; constraints := <?None(); ![] <+ ?Some(WhereClause(constraint)); ![constraint]> whereClause
       ; constraints' := Constraints(<conc> (inlinedSrcVertexConstraints, inlinedEdgeConstraints, inlinedDstVertexConstraints, constraints))
       ; if <?[]> otherEdges
         then path := <to-path(|variable-counter, pathFindingGoal, topKAnyAll)> (n1, n2, edge', constraints', costClause, quantifier)
         else path := ComplexParenthesizedRegularExpressionNotSupported(otherEdges)
         end

  to-path(|variable-counter, pathFindingGoal, topKAnyAll):
    (srcVertex, dstVertex, edge@NormalizedEdge(src, _, dst, direction, _, _), constraints, costClause, quantifier) -> path
    with name := <unique-name(|variable-counter, edge)>
       ; if <?Incoming()> direction
         then vertices := Vertices([Vertex(dst), Vertex(src)])
         else vertices := Vertices([Vertex(src), Vertex(dst)])
         end
       ; edges := Edges([edge])
       ; path-variable-name := <unique-name(|variable-counter, edge)> // for now, all paths are anonymous/generated
       ; commonPathExpression := CommonPathExpression(name, vertices, edges, constraints, costClause)
       ; path := <origin-track-forced(!Path(srcVertex, dstVertex, commonPathExpression, quantifier, path-variable-name, Outgoing(), pathFindingGoal, topKAnyAll))> edge

rules

  norm-has-prop = ?Legacy10Has(varRef, [prop]); !Legacy10Has(varRef, prop) <+
                  ?Legacy10Has(varRef, <id>); map(!Legacy10Has(varRef, <id>)); to-conjunction

rules

  norm-hop-distance = ?ZeroOrMore(); !BetweenNAndM("0", "-1")
  norm-hop-distance = ?OneOrMore(); !BetweenNAndM("1", "-1")
  norm-hop-distance = ?Optional(); !BetweenNAndM("0", "1")
  norm-hop-distance = ?ExactlyN(n); !BetweenNAndM(n, n)
  norm-hop-distance = ?NOrMore(n); !BetweenNAndM(n, "-1")
  norm-hop-distance = ?BetweenZeroAndM(m); !BetweenNAndM("0", m)

rules

  // pgql 1.3
  norm-query(|error-messages, variable-counter, bindVariableCount):
    Query(commonPathExpressions, selectOrModifyClause, graphPatternAndGraphName, groupByClause, havingClause, orderByClause, limitOffsets) ->
      <norm-query(|error-messages, "v1.3", variable-counter, bindVariableCount)> (commonPathExpressions, selectOrModifyClause, optionalGraphName, graphPattern', groupByClause, havingClause, orderByClause, limitOffsets)
    with if <?None()> graphPatternAndGraphName
         then graphPattern' := None()
            ; optionalGraphName := None()
         else <?Some(GraphPatternAndGraphName(graphPattern, optionalGraphName, whereClause))> graphPatternAndGraphName
            ; <?GraphPattern(vertices, connections, Constraints(inlined-constraints))> graphPattern
            ; non-inlined-constraints := <?Some(WhereClause(<id>)); ![<id>] + ?None(); ![]> whereClause
            ; constraints := Constraints(<conc> (inlined-constraints,  non-inlined-constraints))
            ; graphPattern' := Some(GraphPattern(vertices, connections, constraints))
         end

  // pgql 1.1 + 1.2
  norm-query(|error-messages, variable-counter, bindVariableCount):
    Pgql11Query(commonPathExpressions, selectOrModifyClause, fromClause, graphPattern, groupByClause, havingClause, orderByClause, limitOffsets) ->
      <norm-query(|error-messages, "v1.1", variable-counter, bindVariableCount)> (commonPathExpressions, selectOrModifyClause, optionalGraphName, Some(graphPattern), groupByClause, havingClause, orderByClause, limitOffsets)
    with optionalGraphName := <?None() + ?Some(Pgql11FromClause(graphName)); !Some(GraphName(graphName))> fromClause

  // pgql 1.0
  norm-query(|error-messages, variable-counter, bindVariableCount):
    q@Pgql10Query(commonPathExpressions, selectClause, fromClause, graphPattern, groupByClause, orderByClause, limitOffsets) ->
      <norm-query(|error-messages, "v1.0", variable-counter, bindVariableCount)> (commonPathExpressions, selectClause, optionalGraphName, Some(graphPattern), groupByClause, havingClause, orderByClause, limitOffsets)
      with optionalGraphName := <?None() + ?Some(Pgql11FromClause(graphName)); !Some(GraphName(graphName))> fromClause
         ; havingClause := None()

  norm-query(|error-messages, version, variable-counter, bindVariableCount):
    (commonPathExpressions, selectOrModifyClause, optionalGraphName, graphPattern, groupByClause, havingClause, orderByClause, limitOffsets) ->
        NormalizedQuery(commonPathExpressions', selectOrModifyClause', optionalGraphName, graphPattern, groupByClause', havingClause, orderByElems, limitOffsets', error-messages, version, bindVariableCount)
    with
      commonPathExpressions' := <map(norm-common-path-expression(|variable-counter)); !CommonPathExpressions(<id>)> commonPathExpressions;

      expAsGroupVars := <?Some(GroupByClause(<id>)) <+ ![]> groupByClause;
      expAsGroupVars' := <map(norm-ExpAsVar)> expAsGroupVars;
      groupByClause' := <?Some(_); !Some(GroupByClause(expAsGroupVars')) <+ !None()> groupByClause;

      selectOrModifyClause' := <?ModifyClause(_) + ?SelectClause(_, _); (norm-ExpAsVars(|expAsGroupVars', version) <+ norm-select-star(|graphPattern, expAsGroupVars'))> selectOrModifyClause;

      expAsSelectVars := <(?ModifyClause(_)); ![] + ?SelectClause(_, ExpAsVars(<id>))> selectOrModifyClause';
      allExpAsVars := <conc> (expAsGroupVars', <try(?Star(); ![])> expAsSelectVars);
      orderByElems := <try(norm-orderBy(|allExpAsVars, version))> orderByClause;

      limitOffsets' := <norm-limitOffsets> limitOffsets

  norm-ExpAsVars(|expAsGroupVars', version):
    t1@SelectClause(distinct, t2@ExpAsVars(expAsVars)) -> result
    with if <?"v1.0" + ?"v1.1"> version
         then expAsVars' := <map(norm-ExpAsVar)> expAsVars
         else expAsVars' := <map(norm-ExpAsVar-for-select-clause)> expAsVars
         end
       ; result := <origin-track-forced(!SelectClause(distinct, <origin-track-forced(!ExpAsVars(expAsVars'))> t2))> t1 

  norm-ExpAsVar-for-select-clause = ?ExpAsVar(exp@PropRef(_, p), None()); !ExpAsVar(exp, p, NonAnonymous()) <+ norm-ExpAsVar

  norm-ExpAsVar = ?ExpAsVar(exp, Some(VarAssign(v))); !ExpAsVar(exp, v, NonAnonymous())
  norm-ExpAsVar:
    ExpAsVar(exp, None()) -> ExpAsVar(exp, identifier, anonymous)
    with identifier := <(?VarRef(<id>) <+ origin-track-forced(origin-text); origin-track-forced(!Identifier(<id>, <id>)))> exp
       ; anonymous := <?PropRef(_, _); !NonAnonymous() <+ !Anonymous()> exp // 'SELECT n.prop' in PGQL <= 1.2 is 'SELECT n.prop AS "n.prop"' in PGQL 1.3+

  norm-select-star(|graphPattern, group-exps):
    t@SelectClause(distinct, star@Star()) -> result
    with if [] := group-exps
         then vars := <collect(?Vertex(<id>) + ?NormalizedEdge(_, <id>, _, _, _, _) + ?Subquery(_)); filter(Identifier(is-string; not(is-substring(GENERATED)), id))> graphPattern;
              expAsSelectVars := <map(generate-ExpAsVar(|star))> vars
         else expAsSelectVars := star
         end
       ; result := <origin-track-forced(!SelectClause(star, <origin-track-forced(!ExpAsVars(expAsSelectVars))> star))> t

  generate-ExpAsVar(|star):
    t@Identifier(v, originText) -> expAsVar
    with v' := <origin-track-forced(!(v, ""); conc-strings /* generate a new string to make it loose origin */)> star
       ; originText' := <?None() <+ !star; origin-track-forced(!(originText, ""); conc-strings /* generate a new string to make it loose origin */)> originText
       ; newIdentifier := <origin-track-forced(!Identifier(v', originText'))> star
       ; varRef := <origin-track-forced(!VarRef(newIdentifier))> star
       ; expAsVar := <origin-track-forced(!ExpAsVar(varRef, newIdentifier, Anonymous()))> star

  norm-orderBy(|allExpAsVars, version):
    Some(OrderByClause(elems)) -> Some(OrderByClause(elems'))
    with
      elems' := <map(norm-orderBy-ordering(|version))> elems

  norm-orderBy-ordering(|version) =
    ((?Desc(<id>) + ?Legacy10Desc(<id>)); !OrderByElem(<id>, Desc(), version)) <+
    ((try(?Asc(<id>) + ?Legacy10Asc(<id>))); !OrderByElem(<id>, Asc(), version))

  norm-limitOffsets = ?None(); !LimitOffsetClauses(None(), None())
  norm-limitOffsets = ?Some(LimitOffsetClauses(limitClause, offsetClause)); !LimitOffsetClauses(Some(limitClause), offsetClause)
  norm-limitOffsets = ?Some(OffsetLimitClauses(offsetClause, limitClause)); !LimitOffsetClauses(limitClause, Some(offsetClause))
