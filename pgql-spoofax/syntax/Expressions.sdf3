module Expressions

imports

  Names
  Literals
  pgql-lang
  Legacy

template options

  keyword -/- [a-zA-Z0-9]

context-free syntax

  ExpAsVar.ExpAsVar = <<Exp><VarAssign?>>

  Exp = <(<Exp>)> {bracket}
  Exp = PropRef
  Exp = VarRef
  Exp = Literal

  Exp.Not = <NOT <Exp>> {right, case-insensitive}
  Exp.UMin = <-<Exp>> {right}

  Exp.And = <<Exp> AND <Exp>> {left, case-insensitive}
  Exp.Or = <<Exp> OR <Exp>> {left, case-insensitive}
  Exp.Mul = <<Exp> * <Exp>> {left}
  Exp.Add = <<Exp> + <Exp>> {left}
  Exp.Div = <<Exp> / <Exp>> {left}
  Exp.Mod = <<Exp> % <Exp>> {left}
  Exp.Sub = <<Exp> - <Exp>> {left}
  Exp.Eq = <<Exp> = <Exp>> {left}
  Exp.Gt = [[Exp] > [Exp]] {left}
  Exp.Lt = [[Exp] < [Exp]] {left}
  Exp.Gte = [[Exp] >= [Exp]] {left}
  Exp.Lte = [[Exp] <= [Exp]] {left}

  Exp.Neq1 = [[Exp] != [Exp]] {left}
  Exp.Neq2 = [[Exp] <> [Exp]] {left}

  Exp.Cast = <CAST(<Exp> AS <DATA-TYPE>)> {case-insensitive}

  Exp.Exists = <EXISTS<Subquery>> {case-insensitive}
  Exp.ScalarSubquery = Subquery
  Subquery.Subquery = <( <Query> )>

  Exp.IsNull = <<Exp> IS NULL> {right, case-insensitive}
  Exp.IsNotNull = <<Exp> IS NOT NULL> {right, case-insensitive}

context-free syntax // function calls

  Exp.FunctionCall = <<PackageName?><FUNCTION-NAME>(<{Exp ", "}*>)> {case-insensitive, avoid}
  PackageName.PackageName = <<IDENTIFIER>.>

context-free syntax // Aggregates

  Exp.COUNT        = <COUNT(<Distinct?><StarOrExp>)> {case-insensitive}
  Exp.MIN          = <MIN(<Distinct?><Exp>)> {case-insensitive}
  Exp.MAX          = <MAX(<Distinct?><Exp>)> {case-insensitive}
  Exp.SUM          = <SUM(<Distinct?><Exp>)> {case-insensitive}
  Exp.AVG          = <AVG(<Distinct?><Exp>)> {case-insensitive}
  Exp.ARRAY-AGG    = <ARRAY_AGG(<Distinct?><Exp>)> {case-insensitive}

  StarOrExp.Star = <*>
  StarOrExp = <<Exp>>
  Distinct.Distinct = <DISTINCT> {case-insensitive}

context-free syntax // extract

  Exp.ExtractExp = <EXTRACT(<ExtractField> FROM <Exp>)> {case-insensitive}

  ExtractField.Year = <YEAR>
  ExtractField.Month = <MONTH>
  ExtractField.Day = <DAY>
  ExtractField.Hour = <HOUR>
  ExtractField.Minute = <MINUTE>
  ExtractField.Second = <SECOND>
  ExtractField.TimezoneHour = <TIMEZONE_HOUR>
  ExtractField.TimezoneMinute = <TIMEZONE_MINUTE>

context-free priorities

  {
    left:
      Exp.Legacy10Label
      Exp.Legacy10Labels
      Exp.Legacy10HasLabel
      Exp.Legacy10Id
      Exp.Legacy10Has
      Exp.Legacy10InDegree
      Exp.Legacy10OutDegree
  }
  >
  {
    right:
      Exp.UMin
      Exp.Legacy10Not
  }
  >
  {
    left:
      Exp.Mul
      Exp.Div
      Exp.Mod
  }
  >
  {
    left:
      Exp.Add
      Exp.Sub
  }
  >
  {
    left:
      Exp.Eq
      Exp.Neq1
      Exp.Neq2
      Exp.Gt
      Exp.Lt
      Exp.Gte
      Exp.Lte
      Exp.Legacy10Regex
  }
  >
  {
    right:
      Exp.IsNull
      Exp.IsNotNull
  }
  >
  {
    right:
      Exp.Not
  }
  >
  {
    left:
      Exp.And
  }
  >
  {
    left:
      Exp.Or
  }
