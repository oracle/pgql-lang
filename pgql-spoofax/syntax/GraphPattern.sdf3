module GraphPattern

imports
  
  Literals
  Names
  Expressions
  Legacy

template options

  keyword -/- [a-zA-Z0-9]

context-free syntax

  PathPattern.PathPattern = <<Vertex> <EdgeVertex+>>
  PathPattern.SingleVertex = <(<ElemContents>)> // PGQL 1.0
  Vertex.Vertex = <(<ElemContents>)>

  EdgeVertex.EdgeVertex = <<Connection> <Vertex>>
  Connection.OutConn = [-[EdgeOrPathContents?]->[PathQuantifier?]]
  Connection.InConn = [<-[EdgeOrPathContents?]-[PathQuantifier?]]
  Connection.UndirectedEdge = [-[EdgeContents?]-[PathQuantifier?]]
  Connection.OutEdge = [->[PathQuantifier?]]
  Connection.InEdge = [<-[PathQuantifier?]]
  Connection.UndirectedEdge = [-[PathQuantifier?]]

  RelaxedPathPattern.RelaxedPathPattern = <<Vertex?> <RelaxedEdgeVertex+>> // relaxed means that the vertices can be omitted
  RelaxedEdgeVertex.RelaxedEdgeVertex = <<Connection> <Vertex?>>

  Connection.ParenthesizedPath = <( <RelaxedPathPattern> <WhereClause?> <CostClause?> )<PathQuantifier?>>

  PathPattern.Shortest = <<TopK?> SHORTEST ( <PathPattern> )> {case-insensitive}
  PathPattern.Cheapest = <<TopK?> CHEAPEST ( <PathPattern> )> {case-insensitive}
  TopK.TopK = <TOP <UNSIGNED-INT>> {case-insensitive}
  CostClause.CostClause = <COST <Exp>> {case-insensitive}

  EdgeContents.EdgeContents = <[<ElemContents>]>
  EdgeOrPathContents = EdgeContents
  EdgeOrPathContents.Path = </<Identifier?><LabelPredicate><PathQuantifier?>/>

  PathQuantifier.ZeroOrMore = <*>
  PathQuantifier.OneOrMore = <+>
  PathQuantifier.Optional = <?>
  PathQuantifier.ExactlyN = <{<UNSIGNED-INT>}>
  PathQuantifier.NOrMore = <{<UNSIGNED-INT>,}>
  PathQuantifier.BetweenNAndM = <{<UNSIGNED-INT>,<UNSIGNED-INT>}>
  PathQuantifier.BetweenZeroAndM = <{,<UNSIGNED-INT>}>

context-free syntax

  ElemContents.ElemContents  = <<Identifier?><LabelPredicate?><Legacy10Ids?><Legacy10WithInlinedConstraints?>>
  LabelPredicate.Labels      = <:<Labels>>
  Labels.Label               = Identifier
  Labels                     = BindVariable
  Labels.LabelDisjunction    = <<Labels>|<Labels>> {left}
