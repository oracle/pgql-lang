module Legacy

imports

  pgql-lang
  Names
  GraphPattern
  SolutionModifiers
  Literals
  Expressions

template options

  keyword -/- [a-zA-Z0-9]

context-free syntax // PGQL 1.1/1.2

  Legacy11Query.Pgql11Query =
    <
      <{CommonPathExpression "\n"}*>
      <SelectOrModify>
      <Pgql11FromClause?>
      <MatchWhereClauses>
      <GroupByClause?>
      <HavingClause?>
      <OrderByClause?>
      <LimitOffsetClauses?>
    >

  Pgql11FromClause.Pgql11FromClause = <FROM <GraphName>> {case-insensitive}

  MatchWhereClauses.MatchWhereClauses = <MATCH <{PathPattern ","}+> <WhereClause?>> {case-insensitive}

context-free syntax // pgql-lang.sdf3 for PGQL 1.0

  Legacy10Query.Pgql10Query =
    <
      <{CommonPathExpression "\n"}*>
      <SelectClause>
      <Pgql11FromClause?>
      <Legacy10GraphPattern>
      <GroupByClause?>
      <OrderByClause?>
      <LimitOffsetClauses?>
    >

  Legacy10SelectClause.SelectClause = <select <Legacy10Projection>> {case-insensitive}
  Legacy10Projection.ExpAsVars = <<{ExpAsVar ", "}+>>
  Legacy10Projection.Star = <*>

context-free syntax // GraphPattern.sdf3 for PGQL 1.0

  Legacy10GraphPattern.GraphPattern = 
    <
      where
        <{Legacy10PatternElem ",\n"}+>
    > {case-insensitive}

  Legacy10PatternElem = PathPattern

  Legacy10PatternElem.Constraint = Exp {avoid} // see Expressions.sdf3. Note: 'avoid' disambiguates 'select * where (X.p1)' ('(X.p1)' can be a Constraint or a Vertex)

  Legacy10Ids.Ids = <@<AltLit>>
  Legacy10WithInlinedConstraints.InlinedConstraint = < with <{Legacy10InlinedExp ", "}+>> {case-insensitive}
  AltLit.Or              = <<AltLit>|<AltLit>> {left}
  AltLit                 = Literal
  AltLit.String          = REGULAR-IDENTIFIER

  Labels.Legacy10Identifier = STRING

context-free syntax // SolutionModifiers

  OrderElem.Legacy10Asc = <asc(<Exp>)> {case-insensitive}
  OrderElem.Legacy10Desc = <desc(<Exp>)> {case-insensitive}

context-free syntax // Expressions.sdf3

  Exp.Legacy10Not = <!<Exp>> {right}
  Exp.Legacy10Regex = [[Exp] =~ [Exp]] {left, prefer}

  Legacy10InlinedExp = <(<Legacy10InlinedExp>)> {bracket}
  Legacy10InlinedExp = PropRef
  Legacy10InlinedExp = Legacy10DoubleQuotedString1
  Legacy10DoubleQuotedString1.VarRef = Legacy10DoubleQuotedString2
  Legacy10DoubleQuotedString2.DelimitedIdentifier = DELIMITED-IDENTIFIER // VarRef(DelimitedIdentifier(s)) will get normlized to String(s)
  Legacy10InlinedExp = Literal {prefer}
  Legacy10InlinedExp = InlinedCall
  
  Legacy10InlinedExp.SelfPropRef = <<REGULAR-IDENTIFIER>>
  Legacy10InlinedExp.SelfPropRef = <<Dot><REGULAR-IDENTIFIER>>
  
  Legacy10InlinedExp.Legacy10Not = <!<Legacy10InlinedExp>> {right}
  Legacy10InlinedExp.Not = <not <Legacy10InlinedExp>> {right, case-insensitive}
  Legacy10InlinedExp.UMin = <-<Legacy10InlinedExp>> {right}

  Legacy10InlinedExp.And = <<Legacy10InlinedExp> and <Legacy10InlinedExp>> {left, case-insensitive}
  Legacy10InlinedExp.Or = <<Legacy10InlinedExp> or <Legacy10InlinedExp>> {left, case-insensitive}
  Legacy10InlinedExp.Mul = <<Legacy10InlinedExp> * <Legacy10InlinedExp>> {left}
  Legacy10InlinedExp.Add = <<Legacy10InlinedExp> + <Legacy10InlinedExp>> {left}
  Legacy10InlinedExp.Div = <<Legacy10InlinedExp> / <Legacy10InlinedExp>> {left}
  Legacy10InlinedExp.Mod = <<Legacy10InlinedExp> % <Legacy10InlinedExp>> {left}
  Legacy10InlinedExp.Sub = <<Legacy10InlinedExp> - <Legacy10InlinedExp>> {left}
  Legacy10InlinedExp.Eq = <<Legacy10InlinedExp> = <Legacy10InlinedExp>> {left}
  Legacy10InlinedExp.Gt = [[Legacy10InlinedExp] > [Legacy10InlinedExp]] {left}
  Legacy10InlinedExp.Lt = [[Legacy10InlinedExp] < [Legacy10InlinedExp]] {left}
  Legacy10InlinedExp.Gte = [[Legacy10InlinedExp] >= [Legacy10InlinedExp]] {left}
  Legacy10InlinedExp.Lte = [[Legacy10InlinedExp] <= [Legacy10InlinedExp]] {left}

  Legacy10InlinedExp.Neq1 = [[Legacy10InlinedExp] != [Legacy10InlinedExp]] {left}
  Legacy10InlinedExp.Neq2 = [[Legacy10InlinedExp] <> [Legacy10InlinedExp]] {left}

  Legacy10InlinedExp.Legacy10Regex = [[Legacy10InlinedExp] =~ [Legacy10InlinedExp]] {left, prefer}

  InlinedCall.Legacy10Label = <<Obj>label()> {case-insensitive}
  InlinedCall.Legacy10Labels = <<Obj>labels()> {case-insensitive}
  InlinedCall.Legacy10HasLabel = <<Obj>hasLabel(<Exp>)> {case-insensitive}
  InlinedCall.Legacy10Id = <<Obj>id()> {case-insensitive}
  InlinedCall.Legacy10Has = <<Obj>has(<{Exp ", "}*>)> {case-insensitive}
  InlinedCall.Legacy10InDegree  = <<Obj>inDegree()> {case-insensitive}
  InlinedCall.Legacy10OutDegree = <<Obj>outDegree()> {case-insensitive}

  Dot.Dot = <.>
  Obj.Obj = <<VarRef><Dot>>
  Obj.Obj = <<Dot?>>

context-free priorities

  {
    left:
      InlinedCall.Legacy10Label
      InlinedCall.Legacy10Labels
      InlinedCall.Legacy10HasLabel
      InlinedCall.Legacy10Id
      InlinedCall.Legacy10Has
      InlinedCall.Legacy10InDegree
      InlinedCall.Legacy10OutDegree
  }
  >
  {
    right:
      Legacy10InlinedExp.UMin
      Legacy10InlinedExp.Legacy10Not
  }
  >
  {
    left:
      Legacy10InlinedExp.Mul
      Legacy10InlinedExp.Div
      Legacy10InlinedExp.Mod
  }
  >
  {
    left:
      Legacy10InlinedExp.Add
      Legacy10InlinedExp.Sub
  }
  >
  {
    left:
      Legacy10InlinedExp.Eq
      Legacy10InlinedExp.Neq1
      Legacy10InlinedExp.Neq2
      Legacy10InlinedExp.Gt
      Legacy10InlinedExp.Lt
      Legacy10InlinedExp.Gte
      Legacy10InlinedExp.Lte
      Legacy10InlinedExp.Legacy10Regex
  }
  >
  {
    right:
      Legacy10InlinedExp.Not
  }
  >
  {
    left:
      Legacy10InlinedExp.And
  }
  >
  {
    left:
      Legacy10InlinedExp.Or
  }
