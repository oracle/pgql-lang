module modify

language pgql-lang

test Resolve to variables in MATCH [[

  INSERT VERTEX v LABELS ( Person ) PROPERTIES ( v.prop = [[n]].prop ),
         EDGE e BETWEEN [[n]] AND [[n]] LABELS ( knows ) PROPERTIES ( e.prop = [[n]].prop )
  UPDATE n SET PROPERTIES ( [[n]].prop = 123 )
  DELETE [[n]]
  FROM MATCH ([[n]])

]] resolve #1 to #7
   resolve #2 to #7
   resolve #3 to #7
   resolve #4 to #7
   resolve #5 to #7
   resolve #6 to #7

test Resolve to inserted elements [[

  INSERT VERTEX [[v]] LABELS ( Person ) PROPERTIES ( [[v]].prop = n.prop ),
         EDGE [[e]] BETWEEN [[v]] AND [[v]] LABELS ( knows ) PROPERTIES ( [[e]].prop = n.prop )
  UPDATE n SET PROPERTIES ( n.prop = 123 )
  DELETE n
  FROM MATCH (n)

]] resolve #2 to #1
   resolve #6 to #3
   resolve #4 to #1
   resolve #5 to #1

test Subquery with vertex/edge with same name as inserted vertex/edge in outer query [[

  INSERT VERTEX [[v]]
                PROPERTIES ( [[v]].prop = ( SELECT [[v]].prop AS c1 FROM g MATCH ([[v]]) LIMIT 1 ) ),
           EDGE [[e]]
                BETWEEN v AND v
                PROPERTIES ( [[e]].prop = ( SELECT [[e]].prop AS c1 FROM g MATCH () -[ [[e]] ]-> () LIMIT 1 ) )

]] resolve #2 to #1
   resolve #3 to #4
   resolve #6 to #5
   resolve #7 to #8
