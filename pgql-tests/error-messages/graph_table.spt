module variables

language pgql-lang

test Restriction: colon (1) [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH (n[[:]]person)
         WHERE EXISTS ( SELECT * 
                        FROM GRAPH_TABLE (
                               g MATCH (m IS person) -> (o[[:]]person)
                               WHERE element_id(n) = element_id(m)
                               COLUMNS ( m.prop ) ) )
         COLUMNS ( n.prop ) )

]] error like "GRAPH_TABLE restriction: use IS instead of :" at #1, #2

test Restriction: colon (2) [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH (n IS person)
         COLUMNS ( n.prop ) )
     , MATCH (n2[[:]]person) -[e[[:]]knows]-> (n3 IS person)

]] error like "GRAPH_TABLE restriction: use IS instead of :" at #1, #2

test Restriction: graph name [[

  SELECT *
  FROM [[GRAPH_TABLE (
         MATCH (person IS person) -[e]-> ()
         COLUMNS ( person.first_name ) )]]

]] error like "GRAPH_TABLE restriction: graph name required" at #1

test Restriction: subquery without GRAPH_TABLE [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH (person IS person)
         WHERE EXISTS ( SELECT 1
                        FROM [[MATCH (n) ON g,]]
                             [[MATCH ( (n) ) ON g )]]
            OR EXISTS ( [[SELECT 1 FROM g MATCH (n)]] )
            OR EXISTS ( [[SELECT 1 WHERE (n)]] )
         COLUMNS ( person.prop ) )

]] error like "GRAPH_TABLE restriction: wrap top-level MATCH in GRAPH_TABLE" at #1, #2
   error like "Subquery uses older version of PGQL syntax; please make sure that both inner and outer query use the same PGQL version" at #3, #4

test Restriction: vertex sharing with outer query (1) [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH (person IS person)
         WHERE EXISTS ( SELECT *
                        FROM GRAPH_TABLE (
                               g MATCH ([[person]])
                               COLUMNS ( 1 ) ) )
         COLUMNS ( person.first_name ) )

]] error like "GRAPH_TABLE restriction: vertex sharing between inner and outer query is not allowed, introduce a new variable name like person_new and use WHERE vertex_equal(person, person_new)" at #1

test Restriction: vertex sharing with outer query (2) [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH (person IS person)
         WHERE EXISTS ( SELECT *
                        FROM GRAPH_TABLE (
                               g MATCH (n)
                               WHERE EXISTS ( SELECT *
                                             FROM GRAPH_TABLE (
                                               g MATCH ([[person]]) -> ([[n]])
                                               COLUMNS ( 1 ) ) )
                               COLUMNS ( 1 ) ) )
         COLUMNS ( person.first_name ) )

]] error like "GRAPH_TABLE restriction: vertex sharing between inner and outer query is not allowed, introduce a new variable name like person_new and use WHERE vertex_equal(person, person_new)" at #1
   error like "GRAPH_TABLE restriction: vertex sharing between inner and outer query is not allowed, introduce a new variable name like n_new and use WHERE vertex_equal(n, n_new)" at #2

test Restriction: LIMIT [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH (person IS person)
         WHERE EXISTS ( SELECT *
                        FROM GRAPH_TABLE (
                               g MATCH (car IS car)
                               COLUMNS ( 1 )
                             )
                        [[LIMIT 11]] )
         COLUMNS ( person.first_name ) )

]] error like "GRAPH_TABLE restriction: use FETCH FIRST 11 ROWS ONLY instead of LIMIT 11" at #1

test Restriction: LIMIT outsie GRAPH_TABLE [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH (person IS person)
         COLUMNS ( person.first_name ) )
  OFFSET 10
  [[LIMIT 11]]

]] error like "GRAPH_TABLE restriction: use FETCH FIRST 11 ROWS ONLY instead of LIMIT 11" at #1

test Restriction: projecting vertex/edge objects [[

  SELECT [[*]]
  FROM GRAPH_TABLE ( g
         MATCH (n) -[e]-> (m)
         COLUMNS ( n, e, m )
       )

]] error like "GRAPH_TABLE restriction: cannot project vertex or edge objects" at #1

test Restriction: no KEEP clause (1) [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH [[ANY SHORTEST (n)]] -[e]->* (m)
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: use the KEEP clause" at #1

test Restriction: no KEEP clause (2) [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH [[ANY (n)]] -[e]->* (m)
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: use the KEEP clause" at #1

test Restriction: no KEEP clause (3) [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH [[ACYCLIC (n)]] -[e]->* (m)
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: use the KEEP clause" at #1

test Restriction: no KEEP clause (4) [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH [[ALL SHORTEST ACYCLIC PATHS (n)]] -[e]->* (m)
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: use the KEEP clause" at #1

test Restriction: SHORTEST [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH (n) -[e]->* (m)
         [[KEEP SHORTEST
         COLUMNS ( 1 )]]
       )

]] error like "GRAPH_TABLE restriction: use ANY SHORTEST or ALL SHORTEST instead of SHORTEST" at #1

test Restriction: CHEAPEST [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH (n) -[e]->* (m)
         [[KEEP CHEAPEST
         COLUMNS ( 1 )]]
       )

]] error like "GRAPH_TABLE restriction: use ANY CHEAPEST instead of CHEAPEST" at #1


test Restriction: TOP k SHORTEST [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH (n) -[e]->* (m)
         [[KEEP TOP 12 SHORTEST]]
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: use SHORTEST 12 instead of TOP 12 SHORTEST" at #1

test Restriction: label(..) and labels(..) [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH (n)
         COLUMNS ( [[label(n)]] AS lbl, [[labels(n)]] AS lbls )
       )

]] error like "GRAPH_TABLE restriction: cannot use label(..) or labels(..), try using the IS LABELED predicate" at #1, #2

test Restriction: has_label(..) [[

  SELECT *
  FROM GRAPH_TABLE ( g
         MATCH (n)
         COLUMNS ( [[has_label(n, 'PERSON')]], [["HAS_LABEL"(n, 'Car')]], [[Has_Label(n, 'House')]], [["Has_Label"(n, 'House')]], [[Has_Label(n, ?)]] )
       )

]] error like "GRAPH_TABLE restriction: use n IS LABELED " at #1, #2, #3, #4 // Spoofax tests do not support quotes so we can't test for the full message string here
   error like "GRAPH_TABLE restriction: HAS_LABEL function is not supported; try using the IS LABELED predicate" at #5

test Restriction: no COLUMNS clause [[

  SELECT 1
  FROM [[GRAPH_TABLE ( g
         MATCH (n)
       )]]

]] error like "GRAPH_TABLE restriction: COLUMNS clause is required" at #1

test Restriction: ID[[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n) -[e]-> (m)
         WHERE [[id(n)]] > 0 AND [[id(e)]] > 0
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: ID not supported, use VERTEX_ID or EDGE_ID instead" at #1, #2

test Restriction: ALL_DIFFERENT but no vertex/edge input [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n)
         WHERE All_Different([[n.prop1]], [[1]], [['x']], n)
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: ALL_DIFFERENT predicate allows vertex and edge inputs only" at #1, #2, #3

test Restriction: JAVA_REGEXP_LIKE [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n)
         WHERE [[Java_Regexp_Like('aaaaab', 'a*b')]]
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: JAVA_REGEXP_LIKE is unsupported, please try to use another regexp function if one is available or implement a UDF otherwise" at #1

test Restriction: IN_DEGREE and OUT_DEGREE [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n)
         WHERE [[in_degree(n)]] > 10 AND [[out_degree(n)]] > 10
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: IN_DEGREE not supported, use a subquery instead" at #1
   error like "GRAPH_TABLE restriction: OUT_DEGREE not supported, use a subquery instead" at #2

test Restriction: vertex and edge equals [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n) -[e1]-> (m) -[e2]-> (o)
         WHERE [[n = m]] AND [[e1 = e2]]
           AND [[n <> m]] AND [[e1 <> e2]]
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: use VERTEX_EQUAL(n, m)" at #1
   error like "GRAPH_TABLE restriction: use EDGE_EQUAL(e1, e2)" at #2
   error like "GRAPH_TABLE restriction: use NOT VERTEX_EQUAL(n, m)" at #3
   error like "GRAPH_TABLE restriction: use NOT EDGE_EQUAL(e1, e2)" at #4

test Restriction: is_source_of, is_destination_of [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n) -[e]-> ()
         WHERE [[Is_Source_Of(e, n)]]
           AND [[Is_Destination_Of(e, n)]]
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: use n IS SOURCE OF e" at #1
   error like "GRAPH_TABLE restriction: use n IS DESTINATION OF e" at #2

test Restriction: PATH [[

  PATH [[p1]] AS () -[IS knows]-> ()
  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n)
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: path macros not supported" at #1

test Restriction: -/.../-> [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n) [[<-/IS knows*/-]] (m) [[-/IS knows*/->]] (o)
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: -/../-> not supported" at #1, #2

test Restriction: MATCH_NUMBER, MATCHNUM [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n) -[e]->{,10} (m)
         ONE ROW PER VERTEX ( v )
         COLUMNS ( [[MATCH_NUMBER(v)]], [[MATCHNUM(v)]] )
       )

]] error like "GRAPH_TABLE restriction: MATCH_NUMBER not supported, use MATCHNUM instead" at #1
   error like "GRAPH_TABLE restriction: MATCHNUM with argument not supported, use MATCHNUM without argument instead" at #2

test Restriction: MATCHNUM outside GRAPH_TABLE [[

  SELECT [[MATCHNUM()]]
  FROM MATCH (n) -[e]->{,10} (m)
         ONE ROW PER VERTEX ( v )

]] error like "MATCHNUM without argument not supported unless used inside GRAPH_TABLE" at #1

test Restriction: ONE ROW PER EDGE [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH ANY SHORTEST (n) -[e1]->* (m)
         [[ONE ROW PER EDGE ( v1 )]]
         COLUMNS ( 1 )
       )

]] error like "GRAPH_TABLE restriction: ONE ROW PER EDGE not supported, use ONE ROW PER STEP" at #1

test Restriction: INSERT [[

  [[INSERT VERTEX v
  FROM]] GRAPH_TABLE ( g MATCH (n) COLUMNS ( 1 ) )

]] error like "GRAPH_TABLE restriction: INSERT not supported" at #1

test Restriction: PREFIX [[

  SELECT *
  FROM GRAPH_TABLE ( g MATCH (n) COLUMNS ( n.* [[PREFIX 'n_']] ) )

]] error like "GRAPH_TABLE restriction: PREFIX not supported" at #1

test Restriction: aggregation with vertex/edge input [[

  SELECT *
  FROM GRAPH_TABLE ( g MATCH (n) -[e]->{1,4} (m)  COLUMNS ( COUNT([[e]]) AS path_length ) )

]] error like "GRAPH_TABLE restriction: aggregation does not allow vertex or edge input; use the vertex/edge identifier or a property instead" at #1

test Restriction: TIMEZONE [[

  SELECT CAST(time '11:30:00' AS [[TiMe WiTh TiMeZoNe]])
  FROM GRAPH_TABLE ( g MATCH (n) COLUMNS ( n.prop ) )

]] error like "GRAPH_TABLE restriction: TIMEZONE should be TIME ZONE (with space)" at #1

test ONE ROW PER VERTEX without quantifier [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n) [[-[e]-> (m)]]
         ONE ROW PER VERTEX ( v1 )
         COLUMNS ( 1 )
       )

]] error like "Quantifier of the form {1,4} expected" at #1

test ONE ROW PER STEP without quantifier [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH ANY SHORTEST (n) [[-> (m)]]
         ONE ROW PER STEP ( v1, e, v2 )
         COLUMNS ( 1 )
       )

]] error like "Quantifier of the form * or + or {1,4} expected" at #1

test ONE ROW PER VERTEX without quantifier [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n) ->* (m), (m) ->* (o)
         KEEP ANY
         [[ONE ROW PER VERTEX ( v1 )]]
         COLUMNS ( 1 )
       )

]] error like "ONE ROW PER VERTEX or STEP is only permitted if the MATCH clause contains a single path pattern" at #1

test ONE ROW PER STEP without quantifier [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n) ->* (m), (m) ->* (o)
         KEEP ANY
         [[ONE ROW PER STEP ( v1, e, v2 )]]
         COLUMNS ( 1 )
       )

]] error like "ONE ROW PER VERTEX or STEP is only permitted if the MATCH clause contains a single path pattern" at #1

test Only aggregations with group variables allowed in COLUMNS clause (1) [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n)
         COLUMNS ( [[COUNT(*)]] AS aggr1, [[SUM(n.prop)]] AS aggr2 )
       )

]] error like "COLUMNS clause allows only aggregations that access at least one group variable" at #1, #2

test Only aggregations with group variables allowed in COLUMNS clause (2) [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (n) -[e]->{3} (m)
         COLUMNS ( [[COUNT(*)]] AS aggr1, [[SUM(n.prop)]] AS aggr2, SUM(e.prop) AS aggr3 )
       )

]] error like "Aggregation with group variables expected since SELECT clause has another aggregation with group variables" at #1, #2
