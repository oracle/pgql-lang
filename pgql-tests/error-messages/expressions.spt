module select

language pgql-lang

test Illegal separator in LISTAGG [[

  SELECT LISTAGG(1[[, ?]]) AS v1, LISTAGG(1[[, 2]]) AS v2
   MATCH (n) -[e]-> (m)

]] error like "Only string literals are allowed as separator" at #1, #2

test Illegal date time field [[

  SELECT 1
    FROM MATCH (n)
   WHERE n.prop + INTERVAL '2' [[years]] > n.prop2
     AND n.prop + INTERVAL '2' [[MONTHS]] > n.prop2
     AND n.prop + INTERVAL '2' [[days]] > n.prop2
     AND n.prop + INTERVAL '2' [[HOURS]] > n.prop2
     AND n.prop + INTERVAL '2' [[minutes]] > n.prop2
     AND n.prop + INTERVAL '2' [[SECONDS]] > n.prop2

]] error like "Expecting one of YEAR, MONTH, DAY, HOUR, MINUTE, SECOND" at #1, #2, #3, #4, #5, #6

test Illegal date time value [[

  SELECT 1
    FROM MATCH (n)
   WHERE n.prop + INTERVAL [['2.1']] YEAR > n.prop2
     AND n.prop + INTERVAL [['12']] MONTH > n.prop2
     AND n.prop + INTERVAL [['-1']] MONTH > n.prop2
     AND n.prop + INTERVAL [['2.0']] DAY > n.prop2
     AND n.prop + INTERVAL [['-1']] HOUR > n.prop2
     AND n.prop + INTERVAL [['24']] HOUR > n.prop2
     AND n.prop + INTERVAL [['-1']] MINUTE > n.prop2
     AND n.prop + INTERVAL [['60']] MINUTE > n.prop2
     AND n.prop + INTERVAL [['-0.0001']] SECOND > n.prop2
     AND n.prop + INTERVAL [['2E3']] SECOND > n.prop2
     AND n.prop + INTERVAL [['60.0']] SECOND > n.prop2

]] error like "Integer expected" at #1, #4
   error like "Integer between 0 and 11 expected" at #2, #3
   error like "Integer between 0 and 23 expected" at #5, #6
   error like "Integer between 0 and 59 expected" at #7, #8
   error like "Number between 0 and 59.999... expected" at #9, #10, #11

test VERTEX_ID [[

  SELECT 1
    FROM MATCH (v1) -[e1]-> (v2) -[e2]-> ()
   WHERE vertex_id([[1]]) > edge_id(e1)

]] error like "Vertex expected" at #1

test EDGE_ID [[

  SELECT 1
    FROM MATCH (v1) -[e1]-> (v2) -[e2]-> ()
   WHERE vertex_id(v1) > edge_id([[1]])

]] error like "Edge expected" at #1

test VERTEX_EQUAL (1) [[

  SELECT 1
    FROM MATCH (v1) -[e1]-> (v2) -[e2]-> ()
   WHERE vertex_equal([[1]], v2)

]] error like "Vertex expected" at #1

test VERTEX_EQUAL (2) [[

  SELECT 1
    FROM MATCH (v1) -[e1]-> (v2) -[e2]-> ()
   WHERE vertex_equal(v1, [[2]])

]] error like "Vertex expected" at #1

test EDGE_EQUAL (1) [[

  SELECT 1
    FROM MATCH (v1) -[e1]-> (v2) -[e2]-> ()
   WHERE edge_equal([[1]], e2)

]] error like "Edge expected" at #1

test EDGE_EQUAL (2) [[

  SELECT 1
    FROM MATCH (v1) -[e1]-> (v2) -[e2]-> ()
   WHERE edge_equal(e1, [[2]])

]] error like "Edge expected" at #1

test Restriction: MATCH outside of GRAPH_TABLE [[

  SELECT *
  FROM GRAPH_TABLE (
         g MATCH (n IS person)
         COLUMNS ( n.prop ) )
     , [[MATCH (n2) ON g]]

]] error like "GRAPH_TABLE restriction: wrap top-level MATCH in GRAPH_TABLE" at #1
