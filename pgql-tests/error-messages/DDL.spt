module common-path-expressions

language pgql-lang

test Undefined vertex table (1) [[

  CREATE PROPERTY GRAPH MyGraph
    VERTEX TABLES (
      Person
    )
    EDGE TABLES (
      studentOf SOURCE [[Student.Person]] DESTINATION [[University]]
    )

]] error like "Schema name not allowed here" at #1
   error like "Undefined vertex table" at #2

test Undefined vertex table (2) [[

  CREATE PROPERTY GRAPH hr
    VERTEX TABLES (
      employees as "Employee"
    )
    EDGE TABLES (
      employees AS manages SOURCE [[employees]] DESTINATION "Employee"
    )

]] error like "Undefined vertex table" at #1 // should use "Employee" instead

test Duplicate vertex and edge tables [[

  CREATE PROPERTY GRAPH MyGraph
    VERTEX TABLES (
      [[Person]], [[Person]]
    )
    EDGE TABLES (
      [[knows]] SOURCE Person DESTINATION Person,
      [[knows]] SOURCE Person DESTINATION Person
    )

]] error like "Duplicate vertex table name; use an alias to make the vertex table name unique (table AS alias)" at #1, #2
   error like "Duplicate edge table name; use an alias to make the edge table name unique (table AS alias)" at #3, #4

test Complex expressions as property not allowed [[

  CREATE PROPERTY GRAPH myGraph
    VERTEX TABLES (
      Person PROPERTIES ( [["age" + "age" AS prop1,]] [[123 AS prop2]] )
    )
    EDGE TABLES (adsfad
      knows
        SOURCE Person
        DESTINATION Person
        PROPERTIES ( [[weight + 1 AS prop1 )]]
    )

]] error like "Only column references and simple CAST expressions allowed; arbitrary expressions are not yet supported" at #1, #2, #3

test Missing property name [[

  CREATE PROPERTY GRAPH myGraph
    VERTEX TABLES (
      Person PROPERTIES ( [[CAST(age AS INTEGER) )]]
    )
    EDGE TABLES (adsfad
      knows
        SOURCE Person
        DESTINATION Person
        PROPERTIES ( [[CAST(weight AS DOUBLE) )]]
    )

]] error like "Alias required (.. AS name)" at #1, #2

test Duplicate property name [[

  CREATE PROPERTY GRAPH myGraph
    VERTEX TABLES (
      Person PROPERTIES ( [[c1]], c2 AS [[c1]], [[c3]], [[c3]] )
    )
    EDGE TABLES (adsfad
      knows
        SOURCE Person
        DESTINATION Person
        PROPERTIES ( [[c1]], c2 AS [[c1]], [[c3]], [[c3]] )
    )

]] error like "Duplicate property name" at #1, #2, #3, #4, #5, #6, #7, #8

test Missing KEY clause [[

  CREATE PROPERTY GRAPH hr
    VERTEX TABLES ( employees )
    EDGE TABLES (
      employees AS works_for
        [[source employees ( employee_id )]]
        [[destination employees ( employee_id )]]
    )
]] error like "Referenced column list provided but no KEY clause specified" at #1, #2
