module modify

language pgql-lang

test INSERT/UPDATE/DELETE subquery not allowed (1) [[

  SELECT ( [[ UPDATE x SET ( x.prop = 3 )]] FROM MATCH (x) ) AS v
    FROM MATCH(n)

]] error like "SELECT clause expected here" at #1

test INSERT/UPDATE/DELETE subquery not allowed (2) [[

  SELECT n AS n
    FROM MATCH (n)
   WHERE EXISTS ( [[ UPDATE x SET ( x.prop = 3 ) ]] FROM MATCH(x) )

]] error like "SELECT clause expected here" at #1

test Set a property that is grouped by [[

    UPDATE n SET ( [[n.prop]] = 123 )
      FROM MATCH (n)
  GROUP BY n.prop AS nProp

]] error like "Cannot set a property that is grouped by" at #1

test Update property value to vertex or edge (1) [[

  UPDATE n SET ( n.prop1 = [[n]]
                          , n.prop2 = [[m]]
                          , n.prop3 = [[e]] )
    FROM MATCH (n) -[e]-> (m)

]] error like "Cannot set the value of a property to a vertex or an edge" at #1, #2, #3

test Update property value to vertex or edge (2) [[

  UPDATE n SET ( n.prop = ( SELECT [[x AS x]] FROM MATCH (x) LIMIT 1 ) )
    FROM MATCH (n) -[e]-> (m)

]] error like "Scalar subquery not allowed to return a vertex or an edge" at #1

test Update property value to vertex or edge (3) [[

  UPDATE n SET ( n.prop = [[n]] )
    FROM MATCH (n)
  GROUP BY n

]] error like "Cannot set the value of a property to a vertex or an edge" at #1

test Insert element with property value of type vertex or edge (1) [[

  INSERT VERTEX m PROPERTIES ( n.prop1 = [[n]]
                             , n.prop2 = [[m]]
                             , n.prop3 = [[e]] )
    FROM MATCH (n) -[e]-> (m)

]] error like "Cannot set the value of a property to a vertex or an edge" at #1, #2, #3

test Insert element with property value of type vertex or edge (2) [[

  INSERT VERTEX m PROPERTIES ( m.prop = ( SELECT [[x AS x]] FROM MATCH (x) LIMIT 1 ) )
    FROM MATCH (n) -[e]-> (m)

]] error like "Scalar subquery not allowed to return a vertex or an edge" at #1

test Insert element with property value of type vertex or edge (3) [[

    INSERT VERTEX m PROPERTIES ( m.prop = [[n]] )
      FROM MATCH (n)
  GROUP BY n

]] error like "Cannot set the value of a property to a vertex or an edge" at #1

test Set a property multiple times in UPDATE [[

  UPDATE n SET ( [[n.prop]] = 1, [[n.prop]] = 2 )
    FROM MATCH (n)

]] error like "Property is set multiple times" at #1, #2

test Set a property multiple times in INSERT [[

  INSERT VERTEX m PROPERTIES ( [[m.prop]] = 1, [[m.prop]] = 2 )
    FROM MATCH (n)

]] error like "Property is set multiple times" at #1, #2

test Cannot reference inserted elements [[

  INSERT VERTEX v1 LABELS ( Person ),
         VERTEX v2 PROPERTIES ( v2.prop = [[v2]].prop ),
         VERTEX v3,
         VERTEX v4,
         VERTEX v5,
         EDGE e BETWEEN v1 AND v1 LABELS ( knows ) PROPERTIES ( e.prop = [[v4]].prop, [[v5]].prop = 123 ),
         VERTEX v6
  UPDATE n SET ( n.prop1 = [[v6]].prop, n.prop2 = n.prop, m.prop = n.prop )
    FROM MATCH (n) -> (m)

]] error like "Unresolved variable" at #1, #2, #3, #4

test Duplicate variable (1) [[

  INSERT VERTEX [[v1]],
         EDGE [[v1]] BETWEEN v1 AND v1
    FROM MATCH (v1)

]] error like "Duplicate variable: variable already defined in the MATCH clause" at #1, #2

test Duplicate variable (2) [[

    INSERT VERTEX [[v2]],
           EDGE [[v2]] BETWEEN v1 AND v2
      FROM MATCH (v1)
  GROUP BY v1.prop AS v2

]] error like "Duplicate variable: variable already defined in the GROUP BY clause" at #1, #2

test Duplicate variable (3) [[

  INSERT VERTEX [[v2]],
         VERTEX [[v2]],
         EDGE [[v2]] BETWEEN v2 AND v2

]] error like "Duplicate variable" at #1, #2, #3

test Duplicate variable (4) [[

  INSERT VERTEX [[v2]],
         VERTEX [[v2]],
         EDGE [[v2]] BETWEEN v2 AND v2
    FROM MATCH (v1)

]] error like "Duplicate variable" at #1, #2, #3

test Update an insertion (1) [[

  INSERT VERTEX v3
  UPDATE [[v3]] SET ( v3.prop = 3 )

]] error like "Cannot update an element that is being inserted" at #1

test Update an insertion (2) [[

  INSERT VERTEX v3
  UPDATE [[v3]] SET ( v3.prop = 3 )
    FROM g MATCH (v1)

]] error like "Cannot update an element that is being inserted" at #1

test Update a deletion [[

  UPDATE [[n]] SET ( n.prop = 123 )
  DELETE n
    FROM MATCH (n)

]] error like "Element is deleted in DELETE clause" at #1

test Set source of destination of edge to deletion [[

  INSERT edge e BETWEEN [[n]] and [[n]]
  DELETE n
    FROM MATCH (n)

]] error like "Element is deleted in DELETE clause" at #1, #2

test Update other element than the one that was meant to be updated [[

  UPDATE n SET ( [[m]].prop = 3 )
       , m SET ( [[n]].prop = 4 )
    FROM MATCH (n) -> (m)

]] error like "Did you mean n?" at #1
   error like "Did you mean m?" at #2

test Set properties of other element than the one that is being inserted [[

  INSERT VERTEX m PROPERTIES ( [[n]].prop = 3 )
       , EDGE e BETWEEN n AND m PROPERTIES ( [[n]].prop = 4 )
    FROM MATCH (n)

]] error like "Did you mean m?" at #1
   error like "Did you mean e?" at #2

test Duplicate clauses (1) [[

  INSERT VERTEX w
  UPDATE n SET ( n.prop = 123 )
  [[INSERT EDGE e3 BETWEEN m AND o
  DELETE]] e1
  [[DELETE e2
  ]]FROM MATCH (n) -[e1]-> (m) -[e2]-> (o)

]] error like "Only a single INSERT clause is allowed per query; use commas to insert multiple elements in a single INSERT clause" at #1
   error like "Only a single DELETE clause is allowed per query; use commas to delete multiple elements in a single DELETE clause" at #2

test Duplicate clauses (2) [[

  UPDATE n SET ( n.prop = 1 )
  [[UPDATE m SET ( m.prop = 2 )]]
  FROM g MATCH (n) -[e1]-> (m) -[e2]-> (o)

]] error like "Only a single UPDATE clause is allowed per query; use commas to update multiple elements in a single UPDATE clause" at #1

test Duplicate updates or deletions [[

  UPDATE [[v]] SET ( v.first_name = 'Scott' ),
         [[v]] SET ( v.last_name = 'Tiger' )
  DELETE [[w]], [[w]]
  FROM g MATCH (v) -> (w)

]] error like "Element is updated multiple times (merge the two updates together)" at #1, #2
   error like "Element is deleted multiple times" at #3, #4
