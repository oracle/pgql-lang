module syntax

language pgql-lang

test Empty SELECT [[

  SELECT
   FROM g MATCH (x)

]] parse fails

test Empty WHERE [[

  SELECT *
   FROM g MATCH
   MATCH

]] parse fails

test Empty GROUP BY [[

    SELECT *
      FROM g MATCH (n)
  GROUP BY

]] parse fails

test Empty ORDER BY [[

    SELECT *
      FROM g MATCH (n)
  ORDER BY

]] parse fails

test Single-quoted labels [[

  SELECT x AS y
    FROM g MATCH (x:[['lbl']])

]] parse fails

test Invalid date [[

  SELECT [[DATE '2018-13-13']] AS date1
       , [[DATE '']] AS date2
       , [[DATE '12:15:00']] AS date3
    FROM g MATCH (n)

]] error like "Not a valid date: Text '2018-13-13' could not be parsed: Invalid value for MonthOfYear (valid values 1 - 12): 13. An example of a valid date is '2018-01-15'." at #1
   error like "Not a valid date: Text '' could not be parsed at index 0. An example of a valid date is '2018-01-15'." at #2
   error like "Not a valid date: Text '12:15:00' could not be parsed at index 0. An example of a valid date is '2018-01-15'." at #3

test Invalid time [[

  SELECT [[TIME '25:00:00']] AS time1
       , [[TIME '']] AS time2
       , [[TIME '2018-13-13']] AS time3
       , [[TIME '25:00']] AS time4
    FROM g MATCH (n)

]] error like "Not a valid time: Text '25:00:00' could not be parsed: Invalid value for HourOfDay (valid values 0 - 23): 25. Examples of valid times are '16:30:00' and '15:30:00+01:00'." at #1
   error like "Not a valid time: Text '' could not be parsed at index 0. Examples of valid times are '16:30:00' and '15:30:00+01:00'." at #2
   error like "Not a valid time: Text '2018-13-13' could not be parsed at index 2. Examples of valid times are '16:30:00' and '15:30:00+01:00'." at #3
   error like "Not a valid time: Text '25:00' could not be parsed at index 5. Examples of valid times are '16:30:00' and '15:30:00+01:00'." at #4

test Invalid timestamp [[

  SELECT [[TIMESTAMP '25:00:00']] AS timestamp1
       , [[TIMESTAMP '2018-13-13']] AS timestamp2
       , [[TIMESTAMP '']] AS timestamp3
       , [[TIMESTAMP '2018-01-15T16:30:00']] AS timestamp4
    FROM g MATCH (n)

]] error like "Not a valid timestamp: Text '25:00:00' could not be parsed at index 0. Examples of valid timestamps are '2018-01-15 16:30:00' and '2018-01-15 15:30:00+01:00'." at #1
   error like "Not a valid timestamp: Text '2018-13-13' could not be parsed at index 10. Examples of valid timestamps are '2018-01-15 16:30:00' and '2018-01-15 15:30:00+01:00'." at #2
   error like "Not a valid timestamp: Text '' could not be parsed at index 0. Examples of valid timestamps are '2018-01-15 16:30:00' and '2018-01-15 15:30:00+01:00'." at #3
   error like "Not a valid timestamp: Text '2018-01-15T16:30:00' could not be parsed at index 10. Examples of valid timestamps are '2018-01-15 16:30:00' and '2018-01-15 15:30:00+01:00'." at #4

test Missing MATCH clause (1) [[

  SELECT 1 AS c1
    FROM MATCH (n), [[(m)]]

]] error like "Missing MATCH keyword at start of pattern" at #1

test Missing MATCH clause (2) [[

  SELECT 1 AS c1
    FROM [[(n) -> (m)]]

]] error like "Missing MATCH keyword at start of pattern" at #1

test Invalid escaping in string literal [[

  SELECT [['single\'quote']] AS v
    FROM MATCH (n)

]] error like "Use '' instead of \' to escape a single quote" at #1

test Invalid escaping in identifier [[

  SELECT n AS c1
    FROM MATCH (n:[["double\"quote"]]) ON [["double\"quote"]]
   WHERE n.[["double\"quote"]]

]] error like "instead of \" to escape a double quote" at #1, #2, #3
