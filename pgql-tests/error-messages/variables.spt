module variables

language pgql-lang

test Unresolved variable in SELECT [[

  SELECT [[n2]] AS c1
    FROM g MATCH (n)

]] error like "Unresolved variable" at #1

test Unresolved variable in GROUP BY [[

    SELECT n3 AS c1
      FROM g MATCH (n)
  GROUP BY [[n2]] AS n3

]] error like "Unresolved variable" at #1

test Unresolved variable in ORDER BY 1 [[

    SELECT n AS c1
      FROM g MATCH (n)
  ORDER BY [[n2.name]]

]] error like "Unresolved variable" at #1

test Unresolved variable in ORDER BY 2 [[

    SELECT n AS c1
      FROM g MATCH (n)
  GROUP BY n
  ORDER BY [[n2.name]]

]] error like "Unresolved variable (did you mean to use a group key?)" at #1

test Duplicate edge variable (1) [[

  SELECT 123 AS c1
    FROM g MATCH () -[ [[e1]] ]-> () -[ [[e1]] ]-> ()

]] error like "Duplicate variable" at #1, #2

test Duplicate edge variable (2) [[

  SELECT 123 AS c1
    FROM g MATCH ([[e1]]) -[ [[e1]] ]-> ()

]] error like "Duplicate variable" at #1, #2

test Duplicate select key [[

  SELECT n.name AS [[nName]], n.name AS [[nName]]
    FROM g MATCH (n)

]] error like "Duplicate column name in SELECT" at #1, #2

test Zero columns [[

  SELECT [[*]]
    FROM g MATCH ()

]] error like "SELECT * not allowed if there are no variables in the graph pattern" at #1

test Variables in MATCH shadow variables in SELECT [[

    SELECT [[n]].age AS n
      FROM MATCH (n)
  GROUP BY n.prop AS prop

]] error like "Unresolved variable (did you mean to use a group key?)" at #1

test "Repeat subquery from SELECT in GROUP BY and use unnest variable in subquery" [[

    SELECT COUNT(*), (SELECT id(n2) FROM MATCH (n2) WHERE id(n2) = id(k) limit 1) AS x, id([[k]])
      FROM MATCH
             ANY SHORTEST (n) ((src)-[e]->(dst)){2} (m)
             ONE ROW PER VERTEX (k)
  GROUP BY x

]] error like "Unresolved variable (did you mean to use a group key?)" at #1

test Unresolved reference to variable inside table expression [[

  SELECT 1
    FROM MATCH (n)
       , LATERAL ( SELECT e FROM MATCH (x) )
   WHERE n.prop = 3 AND [[x]].prop = 4

]] error like "Unresolved variable" at #1

test Reference with group degree in IS SOURCE/DESTINATION OF [[

  SELECT 1
  FROM GRAPH_TABLE ( g
         MATCH (a IS Account) -[e1 IS transaction]->{4} (a) 
             , (a IS Account) (-[e2 IS transaction]-> (x)) {4} (a) 
         WHERE a IS SOURCE OF [[e1]] AND [[x]] IS NOT DESTINATION OF [[e2]]
         COLUMNS ( e.* ) )

]] error like "Unresolved variable" at #1, #2, #3
